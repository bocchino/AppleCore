:NEW
* -------------------------------
* THE APPLECORE COMPILER, V1.0
* BUILT-IN FUNCTIONS
* -------------------------------
* FN:2 ALLOCATE(S:1)
* RETURN PTR TO S ZEROS ON STACK
* -------------------------------
ALLOCATE
	LDY #6
	LDA (ACC.SP),Y
	STA ACC.SIZE
* SAVE SP INTO IP
	JSR ACC.SET.IP.TO.SP
* PUSH SIZE ZEROS ON STACK
	LDX #0
	JSR ACC.PUSH.X.SIZE.TIMES
* PUSH IP
	LDA ACC.IP
	JSR ACC.PUSH.A
	LDA ACC.IP+1
	JMP ACC.PUSH.A
* -------------------------------
* FN ADD(A:2,B:2,C:2,S:1)
* SET C[0,S]=A[0,S]+B[0,2][0,S]
* -------------------------------
ADD
	JSR AVM.EXECUTE.FN
	ISP $7
	CFD ADD.1
	RAF $0
ADD.1	
	JSR ACC.BUILT.IN.EVAL
	JSR AVM.ADD
	JMP ACC.BUILT.IN.ASSN
* -------------------------------
* FN SUB(A:2,B:2,C:2,S:1)
* SET C[0,S]=A[0,S]-B[2,2][0,S]
* -------------------------------
SUB
	JSR AVM.EXECUTE.FN
	ISP $7
	CFD SUB.1
	RAF $0
SUB.1	
	JSR ACC.BUILT.IN.EVAL
	JSR AVM.SUB
	JMP ACC.BUILT.IN.ASSN
* -------------------------------
* FN MUL(A:2,B:2,C:2,S:1)
* SET C[0,S]=A[0,S]*B[0,S]
* -------------------------------
MUL
	JSR AVM.EXECUTE.FN
	ISP $7
	CFD MUL.1
	RAF $0
MUL.1	
	JSR ACC.BUILT.IN.EVAL
	LDX #0
	JSR AVM.MUL
	JMP ACC.BUILT.IN.ASSN
* -------------------------------
* FN DIV(A:2,B:2,Q:2,R:2,S:1)
* SET Q[0,S]=A[0,S]/B[0,S]
* SET R[0,S]=A[0,S]%B[0,S]
* -------------------------------
DIV
	JSR AVM.EXECUTE.FN
	ISP $9
	CFD DIV.1
	RAF $0
DIV.1	
	JSR ACC.BUILT.IN.EVAL
	LDX #0
	JSR AVM.DIV
* ASSIGN QUOT
        JSR ACC.BUILT.IN.ASSN
* ASSIGN REM
	JSR ACC.SP.UP.SIZE
	JSR ACC.SP.UP.SIZE
	JSR ACC.SP.UP.SIZE
	LDY #6
	JMP ACC.BUILT.IN.ASSN.1
* -------------------------------
* FN:1S CMP(A:2,B:2,S:1)
* COMPARE A[0,S] WITH B[0,S]
* RETURN -1=LT,0=EQ,1=GT
* -------------------------------
CMP
	JSR AVM.EXECUTE.FN
	ISP $5
	CFD CMP.1
	RAF $1
CMP.1	
* EVALUATE ARGS AND COMPARE
	JSR ACC.BUILT.IN.EVAL
	LDX #0
	JSR ACC.CMP
* TEST RESULT
	BEQ .1
	BCS .2
* NEGATIVE, RESULT=-1
	LDA #$FF
	BMI .3
* EQUAL, RESULT=0
.1	LDA #0
	BEQ .3
* POSITIVE, RESULT=1
.2	LDA #1
* PUSH RESULT
.3	JMP ACC.PUSH.A
* -------------------------------
* EVALUATE A AND B
* -------------------------------
ACC.BUILT.IN.EVAL
* SET SIZE
        JSR ACC.POP.A
	STA ACC.SIZE
* EVAL A
	LDY #0
	JSR ACC.SET.IP.TO.VAR
	JSR AVM.MTS.1
* EVAL B AND RETURN
	LDY #2
	JSR ACC.SET.IP.TO.VAR
	JSR AVM.MTS.1
	LDA ACC.SIZE
	RTS
* -------------------------------
* ASSIGN C
* -------------------------------
ACC.BUILT.IN.ASSN
	LDY #4
ACC.BUILT.IN.ASSN.1
	JSR ACC.SET.IP.TO.VAR
	JMP AVM.STM.1

