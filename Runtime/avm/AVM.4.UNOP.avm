:NEW
* -------------------------------
* THE APPLECORE COMPILER, V1.0
* APPLECORE VM, PART 4
* UNARY OPERATIONS
* -------------------------------
* ARITHMETIC NEGATION
* SET SIZE=A
* SET SP[-SIZE,SIZE]*=-1
* -------------------------------
AVM.NEG
	JSR AVM.DSP
	SEC
	BCS AVM.NOT.1
* -------------------------------
* BITWISE NEGATION
* SET SIZE=A
* SET SP[-SIZE,SIZE]=
*	NOT [-SIZE,SIZE]
* -------------------------------
AVM.NOT
	JSR AVM.DSP
	CLC
AVM.NOT.1
	JSR ACC.NOT
	JMP ACC.SP.UP.SIZE
* -------------------------------
* FLIP THE BITS
* -------------------------------
ACC.NOT
	LDX ACC.SIZE
	LDY #0
.1	LDA (ACC.SP),Y
	EOR #$FF
	ADC #0
	STA (ACC.SP),Y
	INY
	DEX
	BNE .1
	RTS
* -------------------------------
* POP IP
* SET SIZE=A
* SET IP[0,SIZE]+=1
* SET SP[0,SIZE]=IP[0,SIZE]
* SET SP+=SIZE
* -------------------------------
AVM.ICR
	STA ACC.SIZE
	TAX
	JSR ACC.POP.IP
	LDY #0
	SEC
.1	LDA (ACC.IP),Y
	ADC #0
	STA (ACC.IP),Y
	INY
	DEX
	BNE .1
	JMP AVM.MTS.1
* -------------------------------
* POP IP
* SET SIZE=A
* SET IP[0,SIZE]-=1
* SET SP[0,SIZE]=IP[0,SIZE]
* SET SP+=SIZE
* -------------------------------
AVM.DCR
	STA ACC.SIZE
	TAX
	JSR ACC.POP.IP
	LDY #0
	CLC
.1	LDA (ACC.IP),Y
	ADC #$FF
	STA (ACC.IP),Y
	INY
	DEX
	BNE .1
	JMP AVM.MTS.1

        