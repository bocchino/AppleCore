* -------------------------------
* THE APPLECORE COMPILER, V1.0
* APPLECORE VM, PART 4
* UNARY OPERATIONS
* -------------------------------
* ARITHMETIC NEGATION
* SET SIZE=A
* SET SP[-SIZE,SIZE]*=-1
* -------------------------------
AVM.NEG
	JSR AVM.DSP
	SEC
	BCS AVM.NOT.1
* -------------------------------
* BITWISE NEGATION
* SET SIZE=A
* SET SP[-SIZE,SIZE]=
*	NOT [-SIZE,SIZE]
* -------------------------------
AVM.NOT
	JSR AVM.DSP
	CLC
AVM.NOT.1
	JSR AVM.NOT.2
	JMP AVM.SP.UP.SIZE
* -------------------------------
* FLIP THE BITS
* -------------------------------
AVM.NOT.2
	LDX AVM.SIZE
	LDY #0
.1	LDA (AVM.SP),Y
	EOR #$FF
	ADC #0
	STA (AVM.SP),Y
	INY
	DEX
	BNE .1
	RTS
* -------------------------------
* POP IP
* SET SIZE=A
* SET IP[0,SIZE]+=1
* -------------------------------
AVM.ICR
	STA AVM.SIZE
	TAX
	JSR AVM.POP.IP
	LDY #0
	SEC
.1	LDA (AVM.IP),Y
	ADC #0
	STA (AVM.IP),Y
	INY
	DEX
	BNE .1
	RTS
* -------------------------------
* POP IP
* SET SIZE=A
* SET IP[0,SIZE]-=1
* -------------------------------
AVM.DCR
	STA AVM.SIZE
	TAX
	JSR AVM.POP.IP
	LDY #0
	CLC
.1	LDA (AVM.IP),Y
	ADC #$FF
	STA (AVM.IP),Y
	INY
	DEX
	BNE .1
	RTS
