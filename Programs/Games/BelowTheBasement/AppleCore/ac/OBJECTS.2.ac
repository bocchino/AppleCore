# -----------------------------------
# BELOW THE BASEMENT: OBJECT MODULE 2
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA              2;
# -----------------------------------
# OBJECT ACTIONS
# -----------------------------------
DATA              SIGN_ACTION;
DATA              PAPER_ACTION;
DATA              DOOR_ACTION;
DATA              WRITING_ACTION;
DATA              FEATHERS_ACTION;
# -----------------------------------
# OBJECT ACTION DATA
# -----------------------------------
DATA READ_SIGN "THE SIGN SAYS: GIVE A HOOT; DON'T POLLUTE.";

FN SIGN_ACTION() {
  IF (VB_ID=VB_READ) 
    RESPOND(READ_SIGN);
  ELSE CANT_TAKE(FIXED);
}    

DATA READ_PAPER "IT SAYS:\$0D\$0DBELOW THE BASEMENT! "\;
DATA            "THE EXCITING FANTASY GAME WHERE YOU CAN ADVENTURE "\;
DATA            "WITHOUT EVER LEAVING YOUR DESK CHAIR!\$0D\$0D"\;
DATA            "CREATED AND WRITTEN BY MAURICE LOTMAN 1984-85.\$0D\$0D"\;
DATA            "APPLECORE PORT AND ENHANCEMENTS BY ROB BOCCHINO 2012.";

DATA PAPER_DESC "IT IS A SMALL SLIP OF PAPER WITH WRITING ON IT.";

FN PAPER_ACTION() {
  IF (VB_ID=VB_LOOK)
    RESPOND(PAPER_DESC);
  IF (VB_ID=VB_READ)
    RESPOND(READ_PAPER);
  TAKE_ACTION();
}

DATA DOOR_DESC "THE WOODEN DOOR IS SET SECURELY IN THE WALL. THERE IS WRITING ON IT.";

FN DOOR_ACTION() {
  IF (VB_ID=VB_OPEN AND 
   CURRENT_RM=RM_FOYER) {
    SET EXIT_DIR=NORTH;
    EXIT_FOYER();
  }
  ELSE IF (VB_ID=VB_LOOK)
    RESPOND(DOOR_DESC);
  ELSE CANT_TAKE(FIXED);
}

DATA WRITING_DESC "IT IS TOO SMALL TO READ WITH THE UNAIDED EYE.";

FN WRITING_ACTION() {
  IF (VB_ID=VB_LOOK OR VB_ID=VB_READ)
    RESPOND(WRITING_DESC);
}

DATA FEATHERS "THERE SURE ARE A LOT OF FEATHERS!";
DATA TOO_MANY_FEATHERS "THERE ARE TOO MANY FEATHERS.";

FN FEATHERS_ACTION() {
  IF (VB_ID=VB_LOOK)
    RESPOND(FEATHERS);
  ELSE CANT_TAKE(TOO_MANY_FEATHERS);
}
