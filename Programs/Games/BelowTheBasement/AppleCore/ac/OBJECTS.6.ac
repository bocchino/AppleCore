# -----------------------------------
# BELOW THE BASEMENT: OBJECT MODULE 6
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA              6;
# -----------------------------------
# OBJECT ACTIONS
# -----------------------------------
DATA              IRON_ACTION;
DATA              SWORD_ACTION;
DATA              DEFAULT_ACTION;
DATA              DEFAULT_ACTION;
DATA              DEFAULT_ACTION;
# -----------------------------------
# OBJECT ACTION DATA
# -----------------------------------
DATA IRON_HEATING "THE IRON IS HEATING UP. "\;
DATA              "WHAT WOULD YOU LIKE TO SOLDER? ";

DATA NEED_SOLDER  "SORRY, YOU'LL NEED SOME SOLDER.";

DATA SOLDER_PIECES "YOU SOLDER THE PIECES TOGETHER TO FORM A "\;
DATA               "KEY IN THE DESIGN OF A SWORD.";

FN IRON_ACTION() {
  VAR USE_ID:1;
  IF (VB_ID=VB_USE) {
    IF (HAVE_OUTLET()) {
      GET_USE_INPUT(IRON_HEATING);
      SET USE_ID=OBJ_ID;
      IF (NOT FIND_OBJ_ID(OBJ_SOLDER))
        RESPOND(NEED_SOLDER);            
      ELSE IF ((USE_ID=OBJ_CROSS AND
       FIND_OBJ_ID(OBJ_METAL)) OR
       (USE_ID=OBJ_METAL AND 
       FIND_OBJ_ID(OBJ_CROSS))) {
        RESPOND(SOLDER_PIECES);
        REMOVE_OBJ_ID(OBJ_CROSS);
        REMOVE_OBJ_ID(OBJ_METAL);
        SET OBJ_ID=OBJ_SWORD;
        ADD_OBJ_TO_RM(TRUE);
      }
    }
  }
  TAKE_ACTION();
}

DATA SWORD_DESC "THIS IS A MINIATURE SWORD. THE END OF THE BLADE "\;
DATA            "FORMS A KEY. THERE IS A SMALL CRACK IN THE BLADE "\;
DATA            "WHERE IT WAS BROKEN AND FUSED BACK TOGETHER.";

FN SWORD_ACTION() {
  LOOK(SWORD_DESC);
  IF (VB_ID=VB_USE AND CURRENT_RM=RM_BARE) {
    PRINT_ENDING();
    SET RESPONDED=TRUE;
    SET DONE=TRUE;
  }
  TAKE_ACTION();
}

DATA END_FILE_NAME "ENDING.TXT";
DATA PARA "\$0D\$0D";
DATA PRESS_KEY "[PRESS A KEY]";

FN PRINT_ENDING() {
  VAR LINES:1=0;
  ISSUE_DOS_CMD(DOS_OPEN,END_FILE_NAME);
  ISSUE_DOS_CMD(DOS_READ,END_FILE_NAME);
  WHILE (DOS_ERRNO[0,1]=0) {
    READ_LINE();
    MON_CROUT();
    IF (DOS_ERRNO[0,1]=0) {
      IF (MON_IN[0,1]=0) {
        SET MON_IN[0,3]=PARA[0,3];
        SET LINES=LINES+2;
      }
      IF (LINES >= MON_WNDBTM[0,1]-3) {
        PRINT_STRING(PARA);
        PRINT_STRING(PRESS_KEY);
        SET ^A=' '+$80;
        MON_KEYIN();
        SET LINES=0;
        POSITION_CURSOR(0,MON_CV[0,1]-2);
      }
      ELSE {
        SET LINES=LINES+STRING_LEN(MON_IN)/40+1;
      }
      PRINT_WORDS(MON_IN);
    }
  }
  PRINT_STRING(DOS_CLOSE);
}
