# -----------------------------------
# BELOW THE BASEMENT: OBJECT MODULE 5
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA              5;
# -----------------------------------
# OBJECT ACTIONS
# -----------------------------------
DATA              SD_ACTION;
DATA              OIL_ACTION;
DATA              JAIL_DOOR_ACTION;
DATA              METAL_OBJECT_ACTION;
DATA              CROSS_ACTION;
# -----------------------------------
# OBJECT ACTION DATA
# -----------------------------------
DATA TIGHTEN "YOU TIGHTEN THE KNOB ON THE CLOSET WITH THE SCREWDRIVER.";

FN SD_ACTION() {
  IF (VB_ID=VB_USE AND CURRENT_RM=RM_CLOSET
   AND SCREW_TIGHTENED=FALSE) {
    RESPOND(TIGHTEN);
    SET SCREW_TIGHTENED=TRUE;
  }
  TAKE_ACTION();
}

DATA USE_OIL "YOU APPLY A FEW DROPS OF OIL TO THE HINGES ON THE DOOR. "\;
DATA         "THE RUST VISIBLY LOOSENS.";

FN OIL_ACTION() {
  IF (VB_ID=VB_USE AND CURRENT_RM=RM_JAIL
   AND NOT JAIL_OILED) {
    RESPOND(USE_OIL);
    SET JAIL_OILED=TRUE;
  }
  TAKE_ACTION();
}

DATA RUSTED_SHUT  "IT IS RUSTED SHUT.";
DATA OPEN_JAIL    "THE OIL DID THE TRICK! THE JAIL DOOR COMES OPEN.";
DATA JAIL_IS_OPEN "THE JAIL DOOR IS OPEN. ";

DATA OBJECT_STUCK "THERE IS A SMALL METAL OBJECT STUCK IN THE KEYHOLE. "\;
DATA              "YOU MIGHT BE ABLE TO TAP IT OUT WITH A HAMMER";
DATA MUST_OPEN    ", BUT YOU'LL HAVE TO OPEN THE DOOR FIRST.";

FN JAIL_DOOR_ACTION() {
  IF (VB_ID=VB_LOOK) {
    IF (JAIL_OPEN)
      RESPOND(JAIL_IS_OPEN);
    IF (NOT HAMMER_USED) {
      RESPOND(OBJECT_STUCK);
      IF (NOT JAIL_OPEN)
        RESPOND(MUST_OPEN);
      ELSE PRINT_CHAR('.');
    }
  }
  IF (VB_ID=VB_OPEN AND NOT JAIL_OPEN) {
    IF (NOT JAIL_OILED)
      RESPOND(RUSTED_SHUT);
    ELSE {
      RESPOND(OPEN_JAIL);
      SET JAIL_OPEN=TRUE;
    }
  }
  CANT_TAKE(FIXED);
}

DATA METAL_DESC "THE METAL OBJECT RESEMBLES THE BLADE OF A SWORD "\;
DATA            "IN MINIATURE. ONE EDGE LOOKS AS IF IT WERE BROKEN.";
DATA METAL_STUCK "IT IS STUCK IN THE KEYHOLE.";

FN METAL_OBJECT_ACTION() {
  LOOK(METAL_DESC);
  IF (HAMMER_USED)
    TAKE_ACTION();
  ELSE
    CANT_TAKE(METAL_STUCK);
}

DATA CROSS_DESC "THE CROSS LOOKS LIKE A SWORD HANDLE IN MINIATURE. "\;
DATA            "THE BLADE END IS BROKEN OFF.";

FN CROSS_ACTION() {
  LOOK(CROSS_DESC);
  TAKE_ACTION();
}
