# -----------------------------------
# BELOW THE BASEMENT: INVENTORY
# -----------------------------------
CONST MAX_CARRIED 5;
# -----------------------------------
# ADD OBJECT
# -----------------------------------
FN ADD_TO_INV():1 {
  IF (NUM_CARRIED=MAX_CARRIED)
    RETURN FALSE;
  SET (@INVENTORY)[NUM_CARRIED,1]=OBJ_ID;
  INCR NUM_CARRIED;
  RETURN TRUE;
}
# -----------------------------------
# REMOVE OBJECT
# -----------------------------------
FN REMOVE_FROM_INV() {
  SET (@INVENTORY)[INV_IDX,1]=
    (@INVENTORY)[NUM_CARRIED-1,1];
  DECR NUM_CARRIED;
}
# -----------------------------------
# FIND OBJECT BY NAME
# -----------------------------------
VAR INV_IDX:1;

FN FIND_OBJ_IN_INV(NAME:@):1 {
  VAR ID:1;
  SET INV_IDX=0;
  WHILE (INV_IDX < NUM_CARRIED) {
    SET ID=(@INVENTORY)[INV_IDX,1];
    IF (TEST_OBJ_NAME(NAME,ID))
      RETURN ID;
    INCR INV_IDX;
  }  
  SET INV_IDX=OBJ_NONE;
  RETURN OBJ_NONE;
}
# -----------------------------------
# FIND OBJECT BY ID
# -----------------------------------
FN FIND_OBJ_ID_IN_INV(OBJ_ID:1):1 {
  SET INV_IDX=0;
  WHILE (INV_IDX < NUM_CARRIED) {
    IF ((@INVENTORY)[INV_IDX,1]=OBJ_ID)
      RETURN TRUE;
    INCR INV_IDX;
  }  
  RETURN FALSE;
}
# -----------------------------------
# LIST ITEMS
# -----------------------------------
DATA CARRYING_NOTHING "YOU AREN'T CARRYING ANYTHING.";
DATA CARRYING         "YOU ARE CARRYING: ";

FN PRINT_INV() {
  VAR I:1=0;
  IF (NUM_CARRIED=0)
    PRINT_WORDS(CARRYING_NOTHING);
  ELSE {
    PRINT_STRING(CARRYING);
    WHILE (I < NUM_CARRIED) {
      IF (I > 0) MON_CROUT();
      POSITION_CURSOR(18,MON_CV[0,1]);
      PRINT_OBJ_NAME((@INVENTORY)[I,1]);    
      INCR I;
    }
  }  
}