# -----------------------------------
# BELOW THE BASEMENT: OBJECT MODULE 4
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA              4;
# -----------------------------------
# OBJECT ACTIONS
# -----------------------------------
DATA              CARD_ACTION;
DATA              BEAR_ACTION;
DATA              MACHINE_ACTION;
DATA              TAKE_ACTION;
DATA              SLOT_ACTION;
# -----------------------------------
# OBJECT ACTION DATA
# -----------------------------------
DATA LOOK_CARD "IT IS A SMALL PLASTIC CARD. "\;
DATA           "THERE IS WRITING ON IT.";

DATA READ_CARD "IT SAYS: BILL FOSTER    USER #32767";

FN CARD_ACTION() {
  LOOK(LOOK_CARD);
  IF (VB_ID=VB_READ)
    RESPOND(READ_CARD);
  IF (VB_ID=VB_USE AND CURRENT_RM=RM_HOT)
    MACHINE_ACTION();
  TAKE_ACTION();
}

DATA KILL_BEAR_1 "YOU CUT HIM SLIGHTLY, BUT HE TAKES A MEAN SWING "\;
DATA             "AND LACERATES YOUR CHEST.";

DATA KILL_BEAR_2 "YOU DRIVE THE KNIFE INTO THE BEAR'S CHEST, "\;
DATA             "AND HE COLLAPSES. "\;
DATA             "YOU SLIT HIS THROAT TO FINISH HIM OFF.";

FN BEAR_ACTION() {
  IF (VB_ID=VB_KILL AND BEAR_ALIVE) {
    IF (HAVE_WEAPON()) {
      SET ATTACKED_BEAR=TRUE;
      IF (MON_RNDL[0,1] OR MISSED_BEAR) {
        RESPOND(KILL_BEAR_2);
        SET BEAR_ALIVE=FALSE;
      }
      ELSE {
        RESPOND(KILL_BEAR_1);
        SET MISSED_BEAR=TRUE;
        INCR NUM_HITS;
      }
    }    
  }
} 

DATA MACHINE_DESC "THE MACHINE IS VERY LARGE AND MADE OF METAL. "\;
DATA              "IT OCCUPIES MOST OF THE WALL. "\;
DATA              "THE FRONT IS ENTIRELY BARE EXCEPT FOR "\;
DATA              "A RECTANGULAR OPENING, SOME LIGHTS, "\;
DATA              "AND A SLOT BELOW THE OPENING.";

DATA MACHINE_WHIRS "THE MACHINE ACCEPTS THE CARD. THE LIGHTS BLINK "\;
DATA               "ON AND OFF, AND THE MACHINE MAKES SOME WHIRRING "\;
DATA               "NOISES. AFTER A FEW SECONDS, ";

DATA GIVE_SD "A SCREWDRIVER APPEARS IN THE WINDOW.";
DATA MACHINE_NOTHING "THE ACTIVITY STOPS.";

DATA NEED_CARD "YOU NEED A CARD.";

FN MACHINE_ACTION() {
  LOOK(MACHINE_DESC);
  IF (VB_ID=VB_USE) {
    IF (NOT FIND_OBJ_ID(OBJ_CARD))
      RESPOND(NEED_CARD);
    ELSE {
      RESPOND(MACHINE_WHIRS);
      IF (NOT SD_GIVEN) {
        RESPOND(GIVE_SD);
        SET OBJ_ID=OBJ_SCREWDRIVER;
        ADD_OBJ_TO_RM(TRUE);
        SET SD_GIVEN=TRUE;
      }
      ELSE
        RESPOND(MACHINE_NOTHING);
    }
  }
}

DATA SLOT_DESC "CAREFUL INSPECTION SHOWS THERE IS WRITING ABOVE THE SLOT. "\;
DATA           "YOU CAN MAKE OUT ONLY THE WORD \$22INSERT\$22; THE REST "\;
DATA           "IS WORN AWAY.";

FN SLOT_ACTION() {
  LOOK(SLOT_DESC);
}
