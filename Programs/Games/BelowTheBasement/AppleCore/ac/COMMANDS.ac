# -----------------------------------
# BELOW THE BASEMENT: COMMANDS
# -----------------------------------
# SHOW THE COMMANDS
# -----------------------------------
DATA CMD_LIST "COMMANDS: N)ORTH S)OUTH E)AST W)EST U)P D)OWN "\;
DATA          "L)OOK I)NVENTORY HELP SAVE RESTORE QUIT\$0D\$0D"\;
DATA          "VERBS: LOOK AT SEARCH EXAMINE INSPECT TAKE GET DROP USE "\;
DATA          "SAY KILL OPEN READ";

FN SHOW_CMDS() {
  PRINT_WORDS(CMD_LIST);
}
# -----------------------------------
# COMMAND MESSAGES
# -----------------------------------
DATA CANT_GO       "YOU CAN'T GO THAT WAY.";
# -----------------------------------
# COMMAND DATA
# -----------------------------------
CONST NUM_CMDS 12;

DATA CMD_NAMES CMD_NAME_QUIT;
DATA           CMD_NAME_N;
DATA           CMD_NAME_S;
DATA           CMD_NAME_E;
DATA           CMD_NAME_W;
DATA           CMD_NAME_U;
DATA           CMD_NAME_D;
DATA           CMD_NAME_L;
DATA           CMD_NAME_I;
DATA           CMD_NAME_HELP;
DATA           CMD_NAME_SAVE;
DATA           CMD_NAME_REST;

DATA CMD_NAME_QUIT  "QUIT";
DATA CMD_NAME_N     "N";
DATA CMD_NAME_S     "S";
DATA CMD_NAME_E     "E";
DATA CMD_NAME_W     "W";
DATA CMD_NAME_U     "U";
DATA CMD_NAME_D     "D";
DATA CMD_NAME_L     "L";
DATA CMD_NAME_I     "I";
DATA CMD_NAME_HELP  "HELP";
DATA CMD_NAME_SAVE  "SAVE";
DATA CMD_NAME_REST  "RESTORE";

DATA CMD_FNS CMD_QUIT_FN;
DATA         CMD_N_FN;
DATA         CMD_S_FN;
DATA         CMD_E_FN;
DATA         CMD_W_FN;
DATA         CMD_U_FN;
DATA         CMD_D_FN;
DATA         CMD_L_FN;
DATA         CMD_I_FN;
DATA         CMD_HELP_FN;
DATA         CMD_SAVE_FN;
DATA         CMD_REST_FN;
# -----------------------------------
# COMMAND FUNCTIONS
# -----------------------------------
FN DO_COMMAND(WORD:@) {
  VAR CMD_NUM:1=FIND_WORD(WORD,
    CMD_NAMES,NUM_CMDS);
  VAR CMD_FN:@;
  IF (CMD_NUM=-1)
    SHOW_CMDS();
  ELSE {
    SET CMD_FN=CMD_FNS[CMD_NUM*2,@];
    CMD_FN();
  }
}
# -----------------------------------
# QUIT
# -----------------------------------
FN CMD_QUIT_FN() {
  SET DONE=TRUE;
}
# -----------------------------------
# N,S,E,W,U,D
# -----------------------------------
FN CMD_N_FN() { GO(NORTH); }
FN CMD_S_FN() { GO(SOUTH); }
FN CMD_E_FN() { GO(EAST); }
FN CMD_W_FN() { GO(WEST); }
FN CMD_U_FN() { GO(UP); }
FN CMD_D_FN() { GO(DOWN); }
# -----------------------------------
# GO TO DIRECTION
# -----------------------------------
VAR EXIT_DIR:1;
VAR EXIT_RM:1;

FN GO(TO:1) {
  SET EXIT_DIR=TO;
  SET EXIT_RM=RM_EXIT(CURRENT_RM,TO);
  IF (CURRENT_RM=RM_RESERVOIR) {
    IF (EXIT_RESERVOIR())
      GO_TO_EXIT_RM();
  }
  ELSE IF (CURRENT_RM=RM_BANK)
    EXIT_BANK();
  ELSE IF (CURRENT_RM=RM_FOYER)
    EXIT_FOYER();
  ELSE IF (CURRENT_RM=RM_BEAR) {
    IF (EXIT_BEAR())
      GO_TO_EXIT_RM();
  }
  ELSE GO_TO_EXIT_RM();
}
# -----------------------------------
# GO TO EXIT ROOM
# -----------------------------------
FN GO_TO_EXIT_RM() {
  IF (EXIT_RM=RM_NONE)
    PRINT_WORDS(CANT_GO);
  ELSE IF (NOT (EXIT_RM=RM_ERROR)) {
    SET CURRENT_RM=EXIT_RM;
    CMD_L_FN();
  }
}
# -----------------------------------
# L)OOK
# -----------------------------------
FN CMD_L_FN() {
  PRINT_RM(CURRENT_RM);
}
# -----------------------------------
# I)NVENTORY
# -----------------------------------
FN CMD_I_FN() {
  PRINT_INV();
}
# -----------------------------------
# HELP
# -----------------------------------
FN CMD_HELP_FN() {
  PRINT_HELP(CURRENT_RM);
}
# -----------------------------------
# SAVE
# -----------------------------------
VAR FILE_BUF:47;
DATA 0;

DATA SAVE_PROMPT "FILE NAME TO SAVE (RET FOR CATALOG)\$0D>";
DATA A_STR       ",A$";
DATA L_STR       ",L$";

FN CMD_SAVE_FN() {
  VAR SAVE_ADDR:@=STATE_BEGIN;
  VAR SAVE_LEN:2=STATE_END-STATE_BEGIN+1;
  VAR LEN:1=GET_FILE_NAME(SAVE_PROMPT);
  IF (LEN > 0) {
    SET (@FILE_BUF)[0,47]=0;
    COPY(MON_IN,LEN,@FILE_BUF);
    COPY(A_STR,3,@FILE_BUF+LEN);
    NUM_TO_STR(@SAVE_ADDR,2,16,@FILE_BUF+3+LEN);
    COPY(L_STR,3,@FILE_BUF+7+LEN);
    NUM_TO_STR(@SAVE_LEN,2,16,@FILE_BUF+10+LEN);
    ISSUE_DOS_CMD(DOS_BSAVE,@FILE_BUF);
  }
}
# -----------------------------------
# RESTORE
# -----------------------------------
DATA REST_PROMPT "FILE NAME TO RESTORE (RET FOR CATALOG)\$0D>";

FN CMD_REST_FN() {
  VAR SAVE_ADDR:@=STATE_BEGIN;
  VAR LEN:1=GET_FILE_NAME(REST_PROMPT);
  IF (LEN > 0) {
    SET (@FILE_BUF)[0,47]=0;
    COPY(MON_IN,LEN,@FILE_BUF);
    COPY(A_STR,3,@FILE_BUF+LEN);
    NUM_TO_STR(@SAVE_ADDR,2,16,@FILE_BUF+3+LEN);
    IF (ISSUE_DOS_CMD(DOS_BLOAD,@FILE_BUF))
      CMD_L_FN();
  }
}
# -----------------------------------
# PROMPT FOR FILE NAME
# -----------------------------------
FN GET_FILE_NAME(PROMPT:@):1 {
  VAR LEN:1;
  PRINT_STRING(PROMPT);
  SET LEN=GET_LINE();
  IF (LEN=0)
    ISSUE_DOS_CMD(DOS_CATALOG,WORD_NULL);
  IF (LEN > 32) RETURN 32;
  RETURN LEN;
}
