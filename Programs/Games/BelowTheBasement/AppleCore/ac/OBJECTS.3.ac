# -----------------------------------
# BELOW THE BASEMENT: OBJECT MODULE 3
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA              3;
# -----------------------------------
# OBJECT ACTIONS
# -----------------------------------
DATA              CAT_ACTION;
DATA              KNIFE_ACTION;
DATA              SCEPTRE_ACTION;
DATA              CORD_ACTION;
DATA              CLEANER_ACTION;
# -----------------------------------
# OBJECT ACTION DATA
# -----------------------------------
DATA LOOK_CAT   "THE CAT STARES BACK AT YOU WITH INTELLIGENCE IN ITS EYES.";
DATA NO_CAT     "THE CAT ISN'T HERE.";
DATA ATTACK_CAT "YOU MOVE TOWARDS THE CAT, BUT BEFORE YOU REACH IT, "\;
DATA            "IT DISAPPEARS. "\;
DATA            "AN ECHOING VOICE SAYS \$22BEWARE!\$22";

FN CAT_ACTION() {
  LOOK(LOOK_CAT);
  IF (VB_ID=VB_KILL) {
    IF (HAVE_WEAPON())
      GO_FOR_CAT();
  }
  ELSE IF (VB_ID=VB_TAKE)
    GO_FOR_CAT();
}

FN GO_FOR_CAT() {
  RESPOND(ATTACK_CAT);
  REMOVE_OBJ_FROM_RM();
}

DATA LOOK_KNIFE "THE KNIFE HAS A LONG BLADE AND APPEARS TO BE "\;
DATA            "A SERVICEABLE WEAPON.";

FN KNIFE_ACTION() {
  LOOK(LOOK_KNIFE);
  TAKE_ACTION();
}

DATA LOOK_SCEPTRE "THE SCEPTRE IS OF SILVER, WITH JEWELS SET "\;
DATA              "IN THE TOP OF THE HANDLE. "\;
DATA              "IT APPEARS VERY VALUABLE.";

FN SCEPTRE_ACTION() {
  LOOK(LOOK_SCEPTRE);
  TAKE_ACTION();
}

DATA USE_CORD "THE CORD IS IN THE OUTLET. "\;
DATA          "WHAT WOULD YOU LIKE TO ATTACH IT TO? ";

DATA VACUUM_ON "THE VACUUM IS ON.";

FN CORD_ACTION() {
  VAR LEN:1;
  IF (VB_ID=VB_USE) {
    IF (HAVE_OUTLET()) {
      GET_USE_INPUT(USE_CORD);
      IF (OBJ_ID=OBJ_VACUUM) {
        RESPOND(VACUUM_ON);
        SET USED_CORD=TRUE;
      }            
    } 
  }
  TAKE_ACTION();
}

DATA USE_VACUUM      "WHAT WOULD YOU LIKE TO VACUUM? ";
DATA CANT_REACH      "YOU PLUG IN THE VACUUM, BUT THE CORD IS NOT "\;
DATA                 "LONG ENOUGH TO REACH THE FEATHERS.";
DATA VACUUM_FEATHERS "AFTER ABOUT TWENTY STROKES, YOU'VE "\;
DATA                 "CLEANED UP ALL THE FEATHERS. "\;
DATA                 "REMAINING ON THE GROUND IS A PLASTIC CARD.";
DATA SURE_GOT        "YOU SURE GOT THAT ";
DATA CLEAN           " CLEAN.";

FN CLEANER_ACTION() {
  IF (VB_ID=VB_USE) {
    SET RESPONDED=TRUE;
    IF (HAVE_OUTLET()) { 
      GET_USE_INPUT(USE_VACUUM);
      IF (OBJ_ID=OBJ_FEATHERS) {
        IF (NOT USED_CORD)
          RESPOND(CANT_REACH);
        ELSE {
          RESPOND(VACUUM_FEATHERS);
          REMOVE_OBJ_ID(OBJ_FEATHERS);  
          SET OBJ_ID=OBJ_CARD;
          ADD_OBJ_TO_RM(TRUE);
        }
      }
      ELSE IF (STRING_LEN(OBJ)>0) {
        RESPOND(SURE_GOT);
        RESPOND(OBJ);
        RESPOND(CLEAN);
      }
    }
  }
  TAKE_ACTION();
}
