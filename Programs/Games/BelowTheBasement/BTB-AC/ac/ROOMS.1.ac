# -----------------------------------
# BELOW THE BASEMENT: ROOM MODULE 1
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA              1;
# -----------------------------------
# ROOM NAMES
# -----------------------------------
DATA	          RM_BRIGHT_NAME;
DATA              RM_RESERVOIR_NAME;
DATA              RM_BANK_NAME;
DATA              RM_FOYER_NAME;
DATA              RM_DARK_NAME;
# -----------------------------------
# ROOM DESCRIPTIONS
# -----------------------------------
DATA              RM_BRIGHT_DESC;
DATA              RM_RESERVOIR_DESC;
DATA              RM_BANK_DESC;
DATA              RM_FOYER_DESC;
DATA              RM_DARK_DESC;
# -----------------------------------
# ROOM EXITS
# -----------------------------------
# BRIGHT
DATA          RM_NONE;
DATA	      RM_NONE;
DATA          RM_MUDDY;
DATA          RM_NONE;
DATA          RM_NONE;
DATA          RM_NONE;

# RESERVOIR
DATA          RM_BANK;
DATA	      RM_NONE;
DATA          RM_NONE;
DATA          RM_MUDDY;
DATA          RM_NONE;
DATA          RM_NONE;

# BANK
DATA          RM_NONE;
DATA	      RM_RESERVOIR;
DATA          RM_FOYER;
DATA          RM_NONE;
DATA          RM_NONE;
DATA          RM_NONE;

# FOYER
DATA          RM_NONE;
DATA	      RM_NONE;
DATA          RM_NONE;
DATA          RM_BANK;
DATA          RM_NONE;
DATA          RM_NONE;

# DARK
DATA          RM_BARE;
DATA	      RM_NONE;
DATA          RM_NONE;
DATA          RM_JAIL;
DATA          RM_FOYER;
DATA          RM_NONE;
# -----------------------------------
# ROOM HELP STATEMENTS
# -----------------------------------
DATA	          RM_BRIGHT_HELP;
DATA              RM_RESERVOIR_HELP;
DATA              RM_BANK_HELP;
DATA              RM_FOYER_HELP;
DATA              RM_DARK_HELP;
# -----------------------------------
# ROOM NAME DATA
# -----------------------------------
DATA RM_BRIGHT_NAME    "BRIGHT ROOM";
DATA RM_RESERVOIR_NAME "RESERVOIR";
DATA RM_BANK_NAME      "EAST BANK";
DATA RM_FOYER_NAME     "FOYER";
DATA RM_DARK_NAME      "DARK CHAMBER";
# -----------------------------------
# ROOM DESCRIPTIONS
# -----------------------------------
DATA BRIGHT_STR "THIS ROOM IS FILLED WITH LIGHT. "\;
DATA            "THERE IS AN EXIT TO THE EAST.";

FN RM_BRIGHT_DESC() {
  PRINT_WORDS(BRIGHT_STR);
}

DATA RESERVOIR_STR "YOU ARE STANDING ON THE LEFT BANK OF A RESERVOIR. "\;
DATA               "THE RESERVOIR RUNS NORTH, AND THERE IS AN EXIT TO THE WEST.";

FN RM_RESERVOIR_DESC() {
  PRINT_WORDS(RESERVOIR_STR);
}

DATA BANK_STR "YOU ARE ON THE EAST BANK OF A RESERVOIR. "\;
DATA          "THE RESERVOIR RUNS SOUTH AND THERE IS AN EXIT TO THE EAST.";

FN RM_BANK_DESC() {
  PRINT_WORDS(BANK_STR);
}

DATA FOYER_STR "THIS IS A SMALL, MUDDY ROOM WITH A FLAGSTONE FLOOR, "\;
DATA           "A DOOR TO THE NORTH, AND AN EXIT TO THE WEST.";

DATA FOYER_STAIR " THERE IS A STAIRCASE LEADING DOWN.";

FN RM_FOYER_DESC() {
  PRINT_WORDS(FOYER_STR);
  IF (FOYER_DOOR_OPEN)
    PRINT_WORDS(FOYER_STAIR);
}

DATA DARK_STR "YOU ARE IN A DARK ROOM THAT HAS EXITS TO THE NORTH AND WEST, "\;
DATA          "AND A STAIRCASE LEADING UP.";

DATA CAT_STR  "\$0D\$0DA BLACK CAT WITH GREEN EYES STARES AT YOU FROM THE SHADOWS.";

FN RM_DARK_DESC() {
  PRINT_WORDS(DARK_STR);
  IF (FIND_OBJ_ID_IN_RM(OBJ_CAT))
    PRINT_WORDS(CAT_STR);
}
# -----------------------------------
# ROOM HELP DATA
# -----------------------------------
DATA RM_BRIGHT_HELP     "A PADDLE MIGHT BE USEFUL...";
DATA RM_RESERVOIR_HELP  "DON'T GET YOUR FEET ALL MUDDY.";
DATA RM_BANK_HELP       "READ THE PAPER.";
DATA RM_FOYER_HELP      "TRY SEARCHING...";
DATA RM_DARK_HELP       "TRY TALKING TO THE CAT.";
# -----------------------------------
# EXIT THE RESERVOIR
# -----------------------------------
FN:1 EXIT_RESERVOIR() {
  IF (EXIT_DIR=NORTH)
    IF (NOT HANDLE_CANOE())
      RETURN FALSE;
  RETURN TRUE;
}
# -----------------------------------
# EXIT THE BANK
# -----------------------------------
FN EXIT_BANK() {
  IF (EXIT_DIR=SOUTH)
    IF (NOT HANDLE_CANOE())
      RETURN;
  GO_TO_EXIT_RM();
}    
# -----------------------------------
# HANDLE THE CANOE
# -----------------------------------
DATA CANOE_HOLE "WHOOPS, THERE IS A HOLE IN YOUR BOAT!  YOU QUICKLY TAKE ON WATER AND SINK. "\;
DATA            "YOU STRUGGLE BACK TO SHORE, BUT YOU ARE NOT GOING ANYWHERE TILL THAT HOLE IS PLUGGED.";

DATA NO_PADDLE "NOW YOU'RE UP A CREEK WITHOUT A PADDLE! "\;
DATA 	       "THE CURRENT IS TAKING YOU DOWNSTREAM. "\;
DATA	       "YOU SEE SOMETHING...IT'S A HUGE ROCK! "\;
DATA	       "I'M SORRY, BUT THIS IS THE END.";

DATA CANOEING  "YOU HOP INTO THE CANOE AND PADDLE TO THE OPPOSITE SHORE.";

FN:1 HANDLE_CANOE() {
  VAR RESULT:1=FALSE;
  IF (NOT FIND_OBJ_ID_IN_INV(OBJ_CANOE))
    RESPOND(CANT_GO);
  ELSE IF (NOT CANOE_PLUGGED)
    RESPOND(CANOE_HOLE);    
  ELSE IF (NOT FIND_OBJ_ID_IN_INV(OBJ_PADDLE)) {
    RESPOND(NO_PADDLE);
    SET DONE=TRUE;
  }
  ELSE {
    RESPOND(CANOEING);
    MON_RDKEY(); 
    SET RESULT=TRUE;
  } 
  RETURN RESULT;
}
# -----------------------------------
# EXIT THE FOYER
# -----------------------------------
DATA DOOR_LOCKED "THE DOOR IS LOCKED.";
DATA DOOR_HELD   "THE DOOR WON'T OPEN.";
DATA DOOR_SWINGS "THE DOOR SWINGS OPEN TO REVEAL A STAIRCASE LEADING DOWN.";

FN EXIT_FOYER() {
  IF (EXIT_DIR=NORTH) {
    IF (FOYER_LOCKED)
      RESPOND(DOOR_LOCKED);
    ELSE IF (FOYER_HELD)
      RESPOND(DOOR_HELD);
    ELSE {
      SET FOYER_DOOR_OPEN=TRUE;
      RESPOND(DOOR_SWINGS);
    }
  }
  ELSE IF (EXIT_DIR=DOWN 
   AND FOYER_DOOR_OPEN) {
    SET EXIT_RM=RM_DARK;
    GO_TO_EXIT_RM();
  }
  ELSE GO_TO_EXIT_RM();
}
# -----------------------------------
# HANDLE SAY IN THE FOYER
# -----------------------------------
DATA FAINT_GROWL "YOU HEAR A FAINT GROWLING IN THE DISTANCE, "\;
DATA 		 "AND THE DOOR MAKES A CLICKING SOUND.";

FN FOYER_SAY_FN() {
  IF (NOT CMP_STRING(OBJ,WORD_BEAR)) {
    IF ((NOT FOYER_LOCKED) AND FOYER_HELD) {
      RESPOND(FAINT_GROWL);
      SET FOYER_HELD=FALSE;    
    }
  }
}
# -----------------------------------
# HANDLE SAY IN THE DARK ROOM
# -----------------------------------
DATA CAT_REAPPEARS "THE CAT REAPPEARS AND SLASHES YOUR FACE.";

DATA CAT_HEARS "THE CAT HEARS YOU AND BEGINS TO SPEAK:\$0D\$0D"\;
DATA 	       "\$22VIOLENCE WILL NOT WORK WITH ME, BUT THE SWORD "\;
DATA	       "HOLDS THE KEY TO THE FUTURE. TAKE YOU THIS SCEPTRE. "\;
DATA	       "IT WILL HELP YOU FIND WHAT YOU ARE LOOKING FOR.\$22\$0D\$0D"\;
DATA	       "A SILVER, JEWELED SCEPTRE APPEARS IN MIDAIR, AND THE "\;
DATA	       "CAT DISAPPEARS.";

FN DARK_SAY_FN() {
  IF (NOT CAT_GONE) {
    IF (NOT FIND_OBJ_ID_IN_RM(OBJ_CAT)) {
      RESPOND(CAT_REAPPEARS);
      SET OBJ_ID=(OBJ_CAT OR OBJ_HIDE_FLAG);
      ADD_OBJ_TO_RM(TRUE);
      INCR NUM_HITS;
    }
    ELSE {
      RESPOND(CAT_HEARS);
      REMOVE_OBJ_FROM_RM();
      SET OBJ_ID=OBJ_SCEPTRE;
      ADD_OBJ_TO_RM(TRUE);
      SET CAT_GONE=TRUE;
    }
  }
}
