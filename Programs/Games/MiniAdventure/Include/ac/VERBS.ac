# -----------------------------------
# MINI ADVENTURE: VERBS
# -----------------------------------
# VERB CODES
# -----------------------------------
CONST NUM_VBS 14;

CONST VB_TAKE    0;
CONST VB_GET     1;
CONST VB_LOOK    2;
CONST VB_SEARCH  3;
CONST VB_EXAMINE 4;
CONST VB_DROP    5;
CONST VB_UNBOLT  6;
CONST VB_LOOSEN  7;
CONST VB_OPEN    8;
CONST VB_SAY     9;
CONST VB_TURN    10;
CONST VB_READ    11;
CONST VB_KILL    12;
CONST VB_CLEAN   13;

DATA VB_NAMES VB_TAKE_NAME;
DATA          VB_GET_NAME;
DATA          VB_LOOK_NAME;
DATA          VB_SEARCH_NAME;
DATA          VB_EXAMINE_NAME;
DATA          VB_DROP_NAME;
DATA          VB_UNBOLT_NAME;
DATA          VB_LOOSEN_NAME;
DATA          VB_OPEN_NAME;
DATA          VB_SAY_NAME;
DATA          VB_TURN_NAME;
DATA          VB_READ_NAME;
DATA          VB_KILL_NAME;
DATA          VB_CLEAN_NAME;
  
DATA VB_TAKE_NAME    "TAKE";
DATA VB_GET_NAME     "GET";
DATA VB_LOOK_NAME    "LOOK";
DATA VB_SEARCH_NAME  "SEARCH";
DATA VB_EXAMINE_NAME "EXAMINE";
DATA VB_DROP_NAME    "DROP";
DATA VB_UNBOLT_NAME  "UNBOLT";
DATA VB_LOOSEN_NAME  "LOOSEN";
DATA VB_OPEN_NAME    "OPEN";
DATA VB_SAY_NAME     "SAY";
DATA VB_TURN_NAME    "TURN";
DATA VB_READ_NAME    "READ";
DATA VB_KILL_NAME    "KILL";
DATA VB_CLEAN_NAME   "CLEAN";

DATA VB_FNS  VB_TAKE_FN;
DATA         VB_TAKE_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_DROP_FN;
DATA         VB_UNBOLT_FN;
DATA         VB_LOOSEN_FN;
DATA         VB_OPEN_FN;
DATA         VB_SAY_FN;
DATA         VB_TURN_FN;
DATA         VB_READ_FN;
DATA         VB_KILL_FN;
DATA         VB_CLEAN_FN;

DATA DONT_SEE "I DON'T SEE ANY ";
DATA HERE     " HERE.";
DATA STR_DONT_US "I DON'T UNDERSTAND ";
DATA STR_USE  "USE";
DATA MORE_SPECIFIC "YOU'LL HAVE TO BE MORE SPECIFIC.";

VAR VB_STR:@;
VAR O1_STR:@;
VAR O2_STR:@;

FN DO_VERB_PHRASE() {
  VAR VB:1;  
  SET VB_STR=(@WORDS)[0,@];
  SET O1_STR=(@WORDS)[2,@];
  SET O2_STR=(@WORDS)[4,@];
  MON_CROUT();
  IF (NOT CMP_STRING(VB_STR,STR_USE)) {
    # "USE X TO Y Z"=>"Y Z WITH X"
    IF (NUM_WORDS=4) {
      SET VB_STR=(@WORDS)[4,@];
      SET O1_STR=(@WORDS)[6,@];
      SET O2_STR=(@WORDS)[2,@];
    }
    ELSE {
      PRINT_WORDS(MORE_SPECIFIC);
      MON_CROUT();
      RETURN;
    }
  }
  SET VB=FIND_WORD(VB_STR,
    VB_NAMES,NUM_VBS);
  SET FOCUS_OBJ=FIND_WORD(O1_STR,
    OBJ_NAMES,NUM_OBJS);
  SET SECOND_OBJ=FIND_WORD(O2_STR,
    OBJ_NAMES,NUM_OBJS);
  IF (VB=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(VB_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (VB=VB_SAY) {
    VB_SAY_FN();
  }
  ELSE IF (FOCUS_OBJ=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(O1_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (NOT IS_HERE(FOCUS_OBJ,
   CURRENT_RM)) {
    PRINT_WORDS(DONT_SEE);
    PRINT_WORDS(O1_STR);
    PRINT_WORDS(HERE);
  }
  ELSE {
    VB_FNS[VB*2,@]();
  }
  MON_CROUT();
}

DATA HAVE_STR "YOU ALREADY HAVE IT.";

FN VB_TAKE_FN() {
  IF (OBJ_RM(FOCUS_OBJ)=RM_TAKEN) {
    PRINT_WORDS(HAVE_STR);
  }
  ELSE {
    OBJ_TAKE_FN(FOCUS_OBJ)();
  }
}

FN VB_LOOK_FN() {
  OBJ_DESC_FN(FOCUS_OBJ)();
}

DATA DONT_HAVE "YOU DON'T HAVE IT.";
DATA DROPPED   "DROPPED";

FN VB_DROP_FN() {
  IF (IS_CARRIED(FOCUS_OBJ)) {
    DROP_ITEM(FOCUS_OBJ);
    PRINT_STRING(DROPPED);
  }
  ELSE {
    PRINT_WORDS(DONT_HAVE);
  }  
}

FN DROP_ITEM(OBJ:1) {
  VAR I:1=0;
  SET (OBJ_RM_PTR(OBJ))[0,1]=
    (CURRENT_RM OR DROPPED_FLAG);
  WHILE (I < NUM_CARRIED) {
    IF (CARRIED_OBJ(I)=OBJ) {
      DECR NUM_CARRIED;
      SET_CARRIED(I,CARRIED_OBJ(NUM_CARRIED));
    }
    INCR I;
  }
}

DATA DOESNT_WORK "THAT DOESN'T WORK.";

DATA UNBOLT "YOU LOOSEN THE BOLTS WITH THE WRENCH.";
DATA ALREADY_UNBOLTED "THE GRATE IS ALREADY UNBOLTED.";
DATA NEED_WRENCH "YOU NEED A WRENCH.";

FN VB_UNBOLT_FN() {
  IF ((FOCUS_OBJ=OBJ_GRATE) OR
   (FOCUS_OBJ=OBJ_BOLTS)) {
     IF (NOT (OBJ_TAKE_FN(OBJ_GRATE)=
      OBJ_TAKE_BOLTED)) {
       PRINT_WORDS(ALREADY_UNBOLTED);
     }
     ELSE IF (NOT IS_CARRIED(OBJ_WRENCH)) {
       PRINT_WORDS(NEED_WRENCH);
     }
     ELSE {     
       PRINT_WORDS(UNBOLT);
       SET (OBJ_TAKE_PTR(OBJ_GRATE))[0,@]=
        OBJ_TAKE_TOO_HEAVY;
       SET (OBJ_TAKE_PTR(OBJ_BOLTS))[0,@]=
        OBJ_TAKE_OK;
       SET (OBJ_RM_PTR(OBJ_BOLTS))[0,1]=
        (RM_ENTER OR DROPPED_FLAG);
     }
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

FN VB_LOOSEN_FN() {
  IF (FOCUS_OBJ=OBJ_BOLTS) {
    VB_UNBOLT_FN();
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA OPEN_GRATE "YOU OPEN THE GRATE, REVEALING A PASSAGE "\;
DATA            "DOWN TO A ROOM BELOW.";
DATA GRATE_OPEN "THE GRATE IS ALREADY OPEN.";
DATA BARREL_ALREADY "THE BARREL IS ALREADY OPEN.";

DATA WONT_BUDGE "THE LID JUST WON'T BUDGE.";
DATA WONT_OPEN  "IT WON'T COME OPEN.";
DATA HATCH_OPENS "YOU FIT THE PLASTER HAND INTO ITS RECEPTACLE. "\;
DATA             "THE LARGE HATCH GLOWS CRIMSON AND OPENS.";
DATA HATCH_OPEN  "THE HATCH IS OPEN.";

FN VB_OPEN_FN() {
  IF (FOCUS_OBJ=OBJ_GRATE) {
    IF (RM_EXIT(RM_ENTER,DOWN)=RM_GRIMY) {
      PRINT_WORDS(GRATE_OPEN);
    }
    ELSE IF (OBJ_TAKE_FN(OBJ_GRATE)=
     OBJ_TAKE_TOO_HEAVY) {
      PRINT_WORDS(OPEN_GRATE);
      SET RM_EXIT_PTR(RM_ENTER,DOWN)[0,1]=
       RM_GRIMY;      
    }
    ELSE OBJ_TAKE_BOLTED();
  }
  ELSE IF (FOCUS_OBJ=OBJ_BARREL) {
    IF (NOT (OBJ_RM(OBJ_SCROLL)=RM_NONE)) {
      PRINT_WORDS(BARREL_ALREADY);
    }
    ELSE PRINT_WORDS(WONT_BUDGE);
  }
  ELSE IF (FOCUS_OBJ=OBJ_HATCH) {
    PRINT_WORDS(HATCH);
  }
  ELSE IF (FOCUS_OBJ=OBJ_SMALL) {
    IF (RM_EXIT(RM_PIPE,WEST)=RM_GRIMY) {
      PRINT_WORDS(WONT_OPEN);
    }
    ELSE {
      PRINT_WORDS(SMALL_OPEN);
    }
  }
  ELSE IF (FOCUS_OBJ=OBJ_LARGE) {
    IF (RM_EXIT(RM_PIPE,UP)=RM_WIN) {
      PRINT_WORDS(HATCH_OPEN);
    }
    ELSE IF (SECOND_OBJ=OBJ_HAND) {
      PRINT_WORDS(HATCH_OPENS);
      SET RM_EXIT_PTR(RM_PIPE,UP)[0,1]=RM_WIN;
    }
    ELSE PRINT_WORDS(WONT_OPEN);
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA ZARTAN "ZARTAN";
DATA DULL_THUD "THE BARREL POPS OPEN WITH A DULL THUD.";
DATA NOTHING "NOTHING HAPPENS.";

FN VB_SAY_FN() {
  IF (NOT CMP_STRING(O1_STR,ZARTAN)) {
    PRINT_WORDS(DULL_THUD);
    SET OBJ_RM_PTR(OBJ_SCROLL)[0,1]=RM_GRIMY;
    SET OBJ_RM_PTR(OBJ_TABLET)[0,1]=RM_GRIMY;
  }
  ELSE {
    PRINT_WORDS(NOTHING);
  }
}

DATA ON  "ON";
DATA OFF "OFF";
DATA LAMP_ON_MSG  "THE LAMP IS NOW ON.";
DATA LAMP_OFF_MSG "THE LAMP IS NOW OFF.";

FN VB_TURN_FN() {
  IF (NOT (FOCUS_OBJ=OBJ_LAMP)) {
    PRINT_WORDS(DOESNT_WORK);
  }
  ELSE IF (NOT CMP_STRING(O2_STR,ON)) {
    PRINT_WORDS(LAMP_ON_MSG);
    SET LAMP_ON=TRUE;
  }
  ELSE IF (NOT CMP_STRING(O2_STR,OFF)) {
    PRINT_WORDS(LAMP_OFF_MSG);
    SET LAMP_ON=FALSE;
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA READ_TABLET "IT SAYS: I AM ZARTAN THE WIZARD. "\;
DATA             "I WAS CONDUCTING A TREASURE-FINDING DEMONSTRATION "\;
DATA             "FOR SOME FRIENDS WHEN I FOUND MYSELF ACCIDENTALLY "\;
DATA             "TELEPORTED TO THIS PLACE. "\;
DATA             "I KNEW THERE WAS GREAT WEALTH HIDDEN WITHIN THESE ROOMS, "\;
DATA             "BUT I WAS UNABLE TO ESCAPE. "\;
DATA             "I FOUND AN IRON GRATE, BUT IT WAS BOLTED FROM THE OUTSIDE."\;
DATA             "\$0D\$0D"\;

DATA             "I RESIGNED MYSELF TO SEARCHING THIS GLOOMY PLACE "\;
DATA             "FOR A MEANS OF ESCAPE. "\;
DATA             "I  LEFT THIS MESSAGE HIDDEN IN A BARREL INACCESSIBLE "\;
DATA             "TO THE BEAST IN THE NEXT ROOM, "\;
DATA             "FOR IT IS NECESSARY TO READ MY NAME TO BREAK THE SPELL, "\;
DATA             "AND TO THE BEST OF MY KNOWLEDGE, OTYUGHS CAN'T READ."\;
DATA             "\$0D\$0D"\;
DATA             "I LEFT IT IN THE HOPE THAT SOMEONE MIGHT FIND IT, "\;
DATA             "SO WE COULD SEARCH FOR A WAY OUT TOGETHER.";

DATA READ_SCROLL "INSCRIBED ON THE PARCHMENT ARE THE MAGICAL WRITINGS "\;
DATA             "FOR A SPELL THAT CLEANS THINGS. "\;
DATA             "IT SEEMS VERY POWERFUL."\;
DATA             "\$0D\$0D"\;
DATA             "STRANGELY ENOUGH, YOU UNDERSTAND HOW TO INVOKE THE SPELL. "\;
DATA             "HOWEVER, YOU ARE CERTAIN THE SCROLL CAN BE USED ONLY ONCE.";

DATA READ_NOTHING "THERE IS NOTHING TO READ.";

FN VB_READ_FN() {
  IF (FOCUS_OBJ=OBJ_TABLET) {
    PRINT_WORDS(READ_TABLET);
  }
  ELSE IF (FOCUS_OBJ=OBJ_SCROLL) {
    PRINT_WORDS(READ_SCROLL);
  }
  ELSE {
    PRINT_WORDS(READ_NOTHING);
  }
}

DATA WONT_HELP "THAT WON'T HELP YOU.";
DATA WONT_WORK "THAT WON'T WORK.";
DATA IDIOT     "YOU TRY TO SQUASH THE SPIDERS WITH YOUR HAND, "\;
DATA           "BUT THEY SCURRY FARTHER UP INTO THE MAZE OF WEBS. "\;
DATA           "YOU HEAR A VAGUE MUTTERING THAT SOUNDS SOMETHING "\;
DATA           "LIKE \$22IDIOT!\$22";
DATA ALREADY_DEAD "THE OTYUGH IS ALREADY DEAD.";
DATA BARE_HANDS "WITH YOUR BARE HANDS?";
DATA NO_KNIFE   "YOU DON'T HAVE A KNIFE!";
DATA KILL_OTY  "YOU SLASH ONE OF ITS TENTACLES AND IT CRIES OUT "\;
DATA           "IN RAGE AND PAIN. YOU THEN DRIVE THE KNIFE "\;
DATA           "INTO ITS GUTS AND FINISH IT OFF.";
DATA WOUND_OTY "YOU GASH ITS LEGS, BUT A MEAN SWIPE OF ITS "\;
DATA           "TENTACLES ALMOST TAKES YOUR HEAD OFF.";

VAR ATTACKED_OT:1=0;

FN VB_KILL_FN() {
  IF (FOCUS_OBJ=OBJ_SPIDERS AND CURRENT_RM=RM_GRIMY) {
    PRINT_WORDS(IDIOT);
  }
  ELSE IF (FOCUS_OBJ=OBJ_OTYUGH) {
    IF (NOT OTYUGH_ALIVE) {
      PRINT_WORDS(ALREADY_DEAD);
    }
    ELSE IF (SECOND_OBJ=OBJ_NONE) {
      PRINT_WORDS(BARE_HANDS);
    }
    ELSE IF (NOT (SECOND_OBJ=OBJ_KNIFE)) {
      PRINT_WORDS(WONT_WORK);
    }
    ELSE IF (NOT IS_HERE(OBJ_KNIFE,CURRENT_RM)) {
      PRINT_WORDS(NO_KNIFE);
    }
    ELSE IF (MON_RNDL[0,1]) {
      PRINT_WORDS(WOUND_OTY);
      SET OT_TURNS=1;
    }
    ELSE {
      PRINT_WORDS(KILL_OTY);
      SET OTYUGH_ALIVE=FALSE;
    }
  }
  ELSE {
    PRINT_WORDS(WONT_HELP);
  }
}

DATA SEVEN_MAIDS "\$22IF SEVEN MAIDS WITH SEVEN MOPS "\;
DATA             "SWEPT IT FOR HALF A YEAR, "\;
DATA             "DO YOU SUPPOSE,\$22 THE WALRUS SAID, "\;
DATA             "\$22THAT THEY COULD GET IT CLEAR?\$22  "\;
DATA             "\$0D\$0D"\;
DATA             "\$22I DOUBT IT,\$22 SAID THE CARPENTER, "\;
DATA             "AND SHED A BITTER TEAR.";

DATA NO_SCROLL "I DON'T SEE ANY SCROLL HERE.";

FN VB_CLEAN_FN() {
  IF (SECOND_OBJ=OBJ_SCROLL) {
    IF (NOT IS_HERE(OBJ_SCROLL,CURRENT_RM)) {
      PRINT_WORDS(NO_SCROLL);
    }
    ELSE USE_SCROLL();
  }
  ELSE IF (FOCUS_OBJ=OBJ_GARBAGE) {
    PRINT_WORDS(SEVEN_MAIDS);
  }
  ELSE {
    PRINT_WORDS(WONT_HELP);
  }
} 

DATA GLOW "YOU READ THE MAGICAL INCANTATIONS, AND THEY "\;
DATA      "GLOW AND WRITHE ON THE PAGE. ";
DATA GARBAGE_GONE "THE GARBAGE DISAPPEARS TO DISCLOSE A PLASTER HAND.";
DATA POWERFUL "THE MAGIC IS POWERFUL AND MAKES THE ";
DATA SPARKLING " SPARKLING CLEAN.";
DATA DUST "\$0D\$0DTHE WRITING DISAPPEARS, AND THE SCROLL TURNS TO DUST.";

FN USE_SCROLL() {
  PRINT_WORDS(GLOW);
  IF (FOCUS_OBJ=OBJ_GARBAGE) {
    PRINT_WORDS(GARBAGE_GONE);
    SET OBJ_RM_PTR(OBJ_GARBAGE)[0,1]=RM_NONE;
    SET OBJ_RM_PTR(OBJ_HAND)[0,1]=
      RM_OFFAL OR DROPPED_FLAG;
  }  
  ELSE {
    PRINT_WORDS(POWERFUL);
    PRINT_WORDS(O1_STR);
    PRINT_WORDS(SPARKLING);
  }
  PRINT_WORDS(DUST);
  IF (IS_CARRIED(OBJ_SCROLL)) {
    DROP_ITEM(OBJ_SCROLL);
  }
  SET OBJ_RM_PTR(OBJ_SCROLL)[0,1]=
    RM_NONE;
}

