# -----------------------------------
# MINI ADVENTURE:  EVENTS
# -----------------------------------
DATA TITLE    "\$0DMINI ADVENTURE: A TEXT ADVENTURE GAME\$0D\$0D";
DATA AUTHOR   "BY ROB BOCCHINO\$0D\$0D";
DATA GREETING "THE PROGRAM UNDERSTANDS SIMPLE ENGLISH SENTENCES "\;
DATA          "AND COMMANDS. TYPE \$22COMMANDS\$22 TO SEE A LIST "\;
DATA          "OF COMMANDS.\$0D\$0D";

FN PRINT_INTRO() {
  INVERSE();
  PRINT_WORDS(TITLE);
  NORMAL();
  PRINT_WORDS(AUTHOR);
  PRINT_WORDS(GREETING);
  PRINT_RM(CURRENT_RM);
}

DATA RUMBLING "\$0DYOU HEAR A RUMBLING ROAR IN THE DISTANCE. "\;
DATA          "SUDDENLY, SOMETHING VERY LARGE IS EATING YOU FOR DINNER.\$0D";

# MAX # OF TURNS ALLOWED IN DARK
CONST DARK_MAX 2;
FN HANDLE_DARK() {
  IF (IN_DARK()) {
    INCR DARK_TURNS;
    IF (DARK_TURNS > DARK_MAX) {
      PRINT_WORDS(RUMBLING);      
      SET DONE=TRUE;
      SET DIED=TRUE;
    }
  }
  ELSE {
    SET DARK_TURNS=0;
  }
}

DATA OT_DIE "\$0DWHILE YOU ARE STANDING THERE, THE OTYUGH ATTACKS "\;
DATA        "WITH ITS TENTACLE. THERE'S NO TIME TO REACT, AND NO ESCAPE. "\;
DATA        "YOU ARE QUICKLY ENVELOPED AND PULLED INTO ITS MAW.\$0D";

# MAX # OF TURNS ALLOWED
CONST OT_MAX 2;
FN HANDLE_OTYUGH() {
  IF (CURRENT_RM=RM_OFFAL AND OTYUGH_ALIVE) {
    INCR OT_TURNS;
    IF (OT_TURNS > OT_MAX) {
      PRINT_WORDS(OT_DIE);
      SET DONE=TRUE;
      SET DIED=TRUE;
    }
  } ELSE {
    SET OT_TURNS=0;
  }
}

FN IN_DARK():1 {
  RETURN (CURRENT_RM > RM_GRIMY) 
   AND ((NOT LAMP_ON) OR (NOT IS_HERE(OBJ_LAMP,CURRENT_RM)));
}

DATA PIPE_MSGS PIPE_SEALED;
DATA           WATER_KNEES;
DATA           WATER_NECK;
DATA           WATER_FILLS;

DATA PIPE_SEALED "SUDDENLY, A DOOR SLAMS DOWN, SEALING OFF THE WEST "\;
DATA             "END OF THE PIPE. A SMALL HATCH OPENS ABOVE YOUR HEAD, "\;
DATA             "AND WATER BEGINS POURING OUT OF IT.";

DATA WATER_KNEES "THE WATER IS UP TO YOUR KNEES.";
DATA WATER_NECK  "THE WATER COVERS YOUR NECK.";
DATA WATER_FILLS "THE WATER FILLS THE ENTIRE PIPE. WITH NO AIR LEFT "\;
DATA             "TO BREATHE, YOU DON'T LAST VERY LONG.";

FN HANDLE_PIPE() {
  IF (CURRENT_RM=RM_PIPE) {
    INCR PIPE_TURNS;
    IF (PIPE_TURNS > 1) {
      SET RM_EXIT_PTR(RM_PIPE,WEST)[0,1]=RM_NONE;
      MON_CROUT();
      PRINT_WORDS(PIPE_MSGS[2*(PIPE_TURNS-2),@]);
      MON_CROUT();
    }
    IF (PIPE_TURNS=5) {
      SET DONE=TRUE;
      SET DIED=TRUE;    
    }
  }
  ELSE SET PIPE_TURNS=0;
}

DATA WIN_MSG_1 "YOU CLIMB OUT OF THE HATCH, YOUR CLOTHES DRIPPING WET. "\;
DATA           "A VOICE SHOUTS, \$22THANK GOODNESS YOU'RE HERE!\$22"\;
DATA           "\$0D\$0DAN AGED AND BENT MAN CLOTHED IN FLOWING ROBES "\;
DATA           "STRIDES OUT TO GREET YOU, HIS ARMS RAISED IN A GESTURE "\;
DATA           "OF FRIENDSHIP."\;

DATA           "\$0D\$0D\$22I SEE YOU FOUND MY BARREL,\$22 HE SAYS, "\;
DATA           "HIS VOICE OVERCOME WITH HAPPINESS. "\;
DATA           "\$22I FOUND THE BARREL THERE, YOU SEE, AND PLACED A "; 
DATA           "POWERFUL SPELL ON IT SO THAT THE OTYUGH COULDN'T GET IN. "\;
DATA           "NOW THAT YOU FOUND ME, IT MAKES EVERYTHING "\;
DATA           "MUCH SIMPLER.\$0D\$0D";

DATA WIN_MSG_2 "\$22I FOUND THE TREASURE.\$22 "\;
DATA           "HE LEADS YOU TO AN ADJOINING ROOM FILLED WITH GOLD "\;
DATA           "AND OTHER RICHES. "\;
DATA           "\$22I ALSO FOUND A WAY OUT OF THIS PLACE. "\;
DATA           "IT REQUIRES TWO TO OPERATE. BUT NOW THAT YOU'RE HERE, "\;
DATA           "WE CAN LEAVE!\$22\$0D\$0D"\;

DATA           "THE TWO OF YOU ESCAPE UNHARMED AND DIVIDE THE TREASURE "\;
DATA           "EQUALLY. YOUR SHARE MAKES YOU VERY RICH INDEED.\$0D\$0D"\;
DATA           "CONGRATULATIONS!\$0D\$0D";

FN PRINT_WIN_MSG() {
  PRINT_WORDS(WIN_MSG_1);
  MON_RDKEY();
  PRINT_WORDS(WIN_MSG_2);
}
