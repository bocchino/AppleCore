:NEW
*--------------------------------
* INSTALL THE SHELL EDITOR IN
* RAM BANK 1
*--------------------------------
INSTALL.EDITOR
* SAVE S REGISTER FOR DOS
	LDA $AA59
	PHA
* READ AND WRITE RAM BANK 1
	LDA RWRAM1
	LDA RWRAM1
* LOAD EDITOR
	JSR PRINT.BLOAD
	LDX #EDITOR.STR
	LDA /EDITOR.STR
	JSR PRINT.STRING
* LOAD DRIVER
	JSR PRINT.BLOAD
	LDX #DRIVER.STR
	LDA /DRIVER.STR
	JSR PRINT.STRING
* PATCH GETLN TO CALL EDITOR
	LDA #$4C
	STA MON.GETLN1
	LDA #INVOKE.EDITOR
	STA MON.GETLN1+1
	LDA /INVOKE.EDITOR
	STA MON.GETLN1+2
* PATCH DOS TO RESTORE BANK 2
	LDA #$4C
	STA $A01B
	LDA #DOS.PATCH
	STA $A01C
	LDA /DOS.PATCH
	STA $A01D
* PATCH RESET TO RESTORE BANK 2
	LDA #RESET
	STA MON.SOFTEV
	LDA /RESET
	STA MON.SOFTEV+1
	JSR MON.SETPWRC
* INITIALIZE THE EDITOR
  	JSR INIT
* CLEAN UP MON.IN
        LDA #$8D
        STA MON.IN
* RESTORE DOS S REGISTER
	PLA
	STA $AA59
* RESTORE BANK 2 NOW AND EXIT
	JMP BANK2
*--------------------------------
* PRINT CTRL-D AND BLOAD
*--------------------------------
PRINT.BLOAD
	LDX #BLOAD.STR
	LDA /BLOAD.STR
	JMP PRINT.STRING
* -------------------------------
* PRINT STRING POINTED TO BY X,A
* CLOBBERS MON.LOC0,MON.LOC1
* -------------------------------
PRINT.STRING
	STX MON.LOC0
	STA MON.LOC1
	LDY #0
.1	LDA (MON.LOC0),Y
	BEQ .2
	ORA #$80
	JSR MON.COUT
	INY
	BNE .1
.2	RTS
*--------------------------------
BLOAD.STR
	.HS 04
	.AS "BLOAD "
	.HS 00
EDITOR.STR
	.AS "SHELL.EDITOR.OBJ"
	.HS 0D
	.HS 00
DRIVER.STR
	.AS "SHELL.EDITOR.DRIVER.OBJ"
	.HS 0D
	.HS 00

