:NEW
*--------------------------------
* INSTALL THE SHELL EDITOR IN
* RAM BANK 1
*--------------------------------
INSTALL.EDITOR
* READ AND WRITE RAM BANK 1
	LDA RWRAM1
	LDA RWRAM1
* LOAD EDITOR
	JSR PRINT.BLOAD
	LDX #EDITOR.STR
	LDA /EDITOR.STR
	JSR PRINT.STRING
* INITIALIZE EDITOR
  	JSR INIT
* PATCH GETLN TO CALL EDITOR
	LDA #$4C
	STA MON.GETLN1
	LDA #DRIVER
	STA MON.GETLN1+1
	LDA /DRIVER
	STA MON.GETLN1+2
* LOAD DRIVER AT $300
	JSR PRINT.BLOAD
	LDX #DRIVER.STR
	LDA /DRIVER.STR
	JSR PRINT.STRING
* MOVE DRIVER INTO PLACE
	LDY #$00
	STY MON.A1L
	LDA #$25
	STA MON.A2L
	LDA #$03
	STA MON.A1H
	STA MON.A2H
	LDA #DRIVER
	STA MON.A4L
	LDA /DRIVER
	STA MON.A4H	
	JSR MON.MOVE
* POINT DOS BUFFERS BELOW DRIVER
  	LDA #$AD
	STA DOS.BUF.ADR
* PATCH DOS TO RESTORE BANK 2
	LDA #$4C
	STA $A01B
	LDA #DOS.PATCH
	STA $A01C
	LDA /DOS.PATCH
	STA $A01D
* PATCH RESET TO RESTORE BANK 2
	LDA #RESET
	STA MON.SOFTEV
	LDA /RESET
	STA MON.SOFTEV+1
        EOR #$A5
        STA MON.PWREDUP
* RESTORE BANK 2 NOW
	JMP BANK2
*--------------------------------
EDITOR.STR
	.AS "SHELL.EDITOR.OBJ"
	.HS 0D
	.HS 00
DRIVER.STR
	.AS "SHELL.EDITOR.DRIVER.OBJ,A$300"
	.HS 0D
	.HS 00


