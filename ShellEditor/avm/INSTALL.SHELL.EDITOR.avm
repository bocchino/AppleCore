:NEW
*--------------------------------
* INSTALL THE SHELL EDITOR IN
* RAM BANK 2
*--------------------------------
INSTALL.EDITOR
* READ AND WRITE RAM BANK 2
	LDA RWRAM2
	LDA RWRAM2
* LOAD EDITOR
	JSR PRINT.BLOAD
	LDX #EDITOR.STR
	LDA /EDITOR.STR
	JSR PRINT.STRING
* LOAD DRIVER
	JSR PRINT.BLOAD
	LDX #DRIVER.STR
	LDA /DRIVER.STR
	JSR PRINT.STRING
* PATCH GETLN TO CALL EDITOR
	LDA #$4C
	STA MON.GETLN
	LDA #INVOKE.EDITOR
	STA MON.GETLN+1
	LDA /INVOKE.EDITOR
	STA MON.GETLN+2
* PATCH DOS TO RESTORE BANK 1
	LDA #$4C
	STA $A01B
	LDA #BANK1
	STA $A01C
	LDA /BANK1
	STA $A01D
* PATCH RESET TO RESTORE BANK 1
	LDA #RESET
	STA MON.SOFTEV
	LDA /RESET
	STA MON.SOFTEV+1
	JSR MON.SETPWRC
* RESTORE BANK 1 NOW AND EXIT
	JMP BANK1
*--------------------------------
* PRINT CTRL-D AND BLOAD
*--------------------------------
PRINT.BLOAD
	LDX #BLOAD.STR
	LDA /BLOAD.STR
	JMP PRINT.STRING
*--------------------------------
BLOAD.STR
	.HS 04
	.AS "BLOAD "
	.HS 00
EDITOR.STR
	.AS "SHELL.EDITOR.OBJ"
	.HS 0D
	.HS 00
DRIVER.STR
	.AS "SHELL.EDITOR.DRIVER.OBJ"
	.HS 0D
	.HS 00

