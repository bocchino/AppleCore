* -------------------------------
* STRING AVM FNS
* -------------------------------
* SEARCH FOR FIRST OCCURRENCE OF
* STRING AT A2 IN STRING AT A1.  
*
* ON EXIT:
* - IF MATCH FOUND, A1 POINTS TO 
*   MATCH.
* - IF MATCH NOT FOUND, A1=$FFFF
* -------------------------------
MATCH.ASM
* SET A3=STRING, A4=PATTERN
	JSR MATCH.INIT.A3.A4
MATCH.LOOP
	LDY #0
	LDA (MON.A4L),Y
* END OF PATTERN: FOUND MATCH
	BEQ MATCH.FOUND
	AND #$7F
	STA AVM.GR
	LDA (MON.A3L),Y
* END OF STRING: NO MATCH
	BEQ MATCH.NOT.FOUND
	AND #$7F
	CMP AVM.GR
	BNE MATCH.UNEQUAL
* NEXT CHAR OF STRING, PATTERN
	JSR MATCH.INC.A3.A4
	JMP MATCH.LOOP
MATCH.NOT.FOUND
	LDA #$FF
	STA MON.A1L
	STA MON.A1H
MATCH.FOUND
	RTS
MATCH.UNEQUAL
* STRING, PATTERN DON'T MATCH:
* ADVANCE POINTER TO STRING AND
* START OVER
	JSR MATCH.INC.A1
	JMP MATCH.ASM
* -------------------------------
MATCH.INIT.A3.A4
	LDY #3
.1	LDA MON.A1L,Y
	STA MON.A3L,Y
	DEY
	BPL .1
	RTS
* -------------------------------
MATCH.INC.A1
	INC MON.A1L
	BNE .1
	INC MON.A1H
.1	RTS
* -------------------------------
MATCH.INC.A3.A4
	INC MON.A3L
	BNE .1
	INC MON.A3H
.1	INC MON.A4L
	BNE .2
	INC MON.A4H
.2	RTS
