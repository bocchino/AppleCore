# -----------------------------------
# EDIFY COMMAND HANDLERS, PART 2
# -----------------------------------
# (M)ATCH
# -----------------------------------
VAR SEARCH_NUM:2;
VAR SEARCH_PTR:2;

FN M_HANDLER() {
  VAR MATCH_PTR:2;
  IF (EQ_CHAR(READ_CHAR(),BINDING_CHAR)) {
    NEXT_CHAR();
    IF (READ_CHAR()=NO_CHAR) {
      ERROR(EXPECTED_PATTERN);
      RETURN;
    }
    SET PATTERN=@INPUT+INPUT_IDX;
    SET INPUT_IDX=INPUT_LEN;
    SET SEARCH_NUM=1;
    SET SEARCH_PTR=BUF_START;
  }
  ELSE IF (PATTERN=NO_PATTERN) {
    ERROR(PATTERN_NOT_SET);
    RETURN;
  }
  WHILE (SEARCH_NUM <= BUF_SIZE) {
    SET MATCH_PTR=
      MATCH(SEARCH_PTR+1,PATTERN);
    IF (NOT (MATCH_PTR=-1))
      PRINT_LINE(SEARCH_PTR,SEARCH_NUM);
    NEXT_LINE_PTR(@SEARCH_PTR);
    INCR SEARCH_NUM;
    IF (NOT (MATCH_PTR=-1))
      RETURN;
  }
}
# -----------------------------------
# CURRENT LI(N)E
# -----------------------------------
FN N_HANDLER() {
  VAR NEW_N:2;
  IF (EQ_CHAR(READ_CHAR(),BINDING_CHAR)) {
    NEXT_CHAR();
    SET NEW_N=PARSE_NUM();
    IF (NOT ERR_FLAG) {
      IF (NEW_N < 1 OR NEW_N > BUF_SIZE)
        ERROR(OUT_OF_RANGE);
      ELSE
        SET_N(NEW_N);
    }
  }
  ELSE IF (BUF_SIZE=0) {
    ERROR(BUFFER_EMPTY);
  }
  ELSE {
    PRINT_LINE(N_PTR,N);
  }
}
# -----------------------------------
# (R)EPLACE
# -----------------------------------
FN R_HANDLER() {
  TODO();
}
# -----------------------------------
# (S)AVE
# -----------------------------------
FN S_HANDLER() {
  IF ((@FILE_NAME)[0,1]=0)
    ERROR(FILE_NAME_NOT_SET);
  ELSE IF (BUF_SIZE=0)
    ERROR(BUFFER_EMPTY);
  ELSE {
    WRITE_BUFFER();
    SET MODIFIED=FALSE;
  }
}
# -----------------------------------
# (V)IEW
# -----------------------------------
FN V_HANDLER() {
  VAR I:2=0;
  VAR LINE_PTR:2=N_PTR;
  IF (BUF_SIZE=0) {
    ERROR(BUFFER_EMPTY);
    RETURN;
  }
  WHILE (I < W AND (N+I<=BUF_SIZE)) {
    PRINT_LINE(LINE_PTR,N+I);
    NEXT_LINE_PTR(@LINE_PTR);
    INCR I;
  }
}
# -----------------------------------
# (W)INDOW SIZE
# -----------------------------------
DATA WINDOW_SIZE "WINDOW SIZE=";

FN W_HANDLER() {
  VAR NEW_W:2;
  IF (EQ_CHAR(READ_CHAR(),BINDING_CHAR)) {
    NEXT_CHAR();
    SET NEW_W=PARSE_NUM();
    IF (NOT ERR_FLAG) {
      IF (NEW_W=0)
        ERROR(OUT_OF_RANGE);
      ELSE
        SET W=NEW_W;
    }    
  }
  ELSE {
    PRINT_STRING(WINDOW_SIZE);
    PRINT_NUM(W);
    MON_CROUT();
  }
}
# -----------------------------------
# E(X)TRACT
# -----------------------------------
FN X_HANDLER() {
  TODO();
}
# -----------------------------------
# COP(Y)
# -----------------------------------
FN Y_HANDLER() {
  TODO();
}
# -----------------------------------
# '+'
# -----------------------------------
FN PLUS_HANDLER() {
  IF (BUF_SIZE=0)
    ERROR(BUFFER_EMPTY);
  ELSE {
    IF (N+W > BUF_SIZE)
      SET_N(BUF_SIZE);
    ELSE SET_N(N+W);
  }
}
# -----------------------------------
# '-'
# -----------------------------------
FN MINUS_HANDLER() {
  IF (BUF_SIZE=0)
    ERROR(BUFFER_EMPTY);
  ELSE {
    IF (N <= W)
      SET_N(1);
    ELSE SET_N(N-W);
  }
}
# -----------------------------------
# '#'
# -----------------------------------
FN NUM_HANDLER() {
  SET PRINT_LINE_NUMS=
    NOT PRINT_LINE_NUMS;
}
