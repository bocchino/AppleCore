# -----------------------------------
# EDIFY: DISK OPERATIONS
# -----------------------------------
# DELETE FILE FROM DISK, IF THERE
# -----------------------------------
FN DELETE_FILE() {
  PRINT_STRING(DOS_DELETE);
  PRINT_STRING(@FILE_NAME);
  MON_CROUT();
}
# -----------------------------------
# WRITE BUFFER TO DISK
# -----------------------------------
FN WRITE_BUFFER() {
  VAR I:2=0;
  VAR LINE_PTR:2=BUF_START;
  DELETE_FILE();
  ISSUE_DOS_CMD(DOS_OPEN,@FILE_NAME);
  ISSUE_DOS_CMD(DOS_WRITE,@FILE_NAME);
  IF (NOT ERR_FLAG) {
    WHILE (I < BUF_SIZE) {
      PRINT_STRING(LINE_PTR+1);
      MON_CROUT();
      NEXT_LINE_PTR(@LINE_PTR);
      INCR I;
    }
    PRINT_STRING(DOS_CLOSE);
  }
}
# -----------------------------------
# READ BUFFER FROM DISK
# -----------------------------------
FN READ_BUFFER() {
  VAR LEN:1=0;
  ISSUE_DOS_CMD(DOS_OPEN,@FILE_NAME);
  ISSUE_DOS_CMD(DOS_READ,@FILE_NAME);
  IF (NOT ERR_FLAG) {
    WHILE (DOS_ERRNO[0,1]=0) {
      SET LEN=READ_LINE();
      MON_CROUT();
      IF (DOS_ERRNO[0,1]=0)
        BUF_APPEND_LINE(MON_IN,LEN);
    }
    PRINT_STRING(DOS_CLOSE);
  }
}
# -----------------------------------
# ISSUE A DOS COMMAND
# -----------------------------------
FN ISSUE_DOS_CMD(CMD:2,FNAME:2) {
  VAR ERR:1;
  IF (NOT ERR_FLAG) {
    SET DOS_ERRNO[0,1]=0;
    PRINT_STRING(CMD);
    PRINT_STRING(FNAME);
    MON_CROUT();
    SET ERR=DOS_ERRNO[0,1];
    IF (ERR=DOS_NOT_FOUND)
      ERROR(FILE_NOT_FOUND);
    ELSE IF (ERR > 0)
      ERROR(DISK_ERROR);
  }
}
