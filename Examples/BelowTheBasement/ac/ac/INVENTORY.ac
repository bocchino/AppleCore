# -----------------------------------
# BELOW THE BASEMENT: INVENTORY
# -----------------------------------
CONST MAX_CARRIED 5;

VAR NUM_CARRIED:1=0;
VAR INVENTORY:5;

DATA TOO_MANY "YOU ARE CARRYING TOO MANY ITEMS.";
# -----------------------------------
# ADD OBJECT
# -----------------------------------
FN ADD_TO_INV() {
  IF (NUM_CARRIED=MAX_CARRIED)
    PRINT_WORDS(TOO_MANY);
  ELSE {
    SET (@INVENTORY)[NUM_CARRIED,1]=OBJ_ID;
    INCR NUM_CARRIED;
  }
}
# -----------------------------------
# REMOVE OBJECT
# -----------------------------------
FN REMOVE_FROM_INV() {
  SET (@INVENTORY)[INV_IDX,1]=
    (@INVENTORY)[NUM_CARRIED-1,1];
  DECR NUM_CARRIED;
}
# -----------------------------------
# FIND OBJECT
# -----------------------------------
VAR INV_IDX:1;

FN:1 FIND_OBJ_IN_INV(OBJ:2) {
  VAR OBJ_IDX:1;
  VAR NAME_PTR:2;
  SET INV_IDX=0;
  WHILE (INV_IDX < NUM_CARRIED) {
    SET OBJ_IDX=(@INVENTORY)[INV_IDX,1];
    SET NAME_PTR=OBJ_NAMES+4*OBJ_IDX;
    IF ((NOT CMP_STRING(OBJ,NAME_PTR[0,2]))
      OR (NOT CMP_STRING(OBJ,NAME_PTR[2,2])))
        RETURN OBJ_IDX;
    INCR INV_IDX;
  }  
  SET INV_IDX=OBJ_NONE;
  RETURN OBJ_NONE;
}
# -----------------------------------
# LIST ITEMS
# -----------------------------------
DATA CARRYING_NOTHING "YOU AREN'T CARRYING ANYTHING.";
DATA CARRYING "YOU ARE CARRYING: ";

FN PRINT_INV() {
  VAR I:1=0;
  IF (NUM_CARRIED=0)
    PRINT_WORDS(CARRYING_NOTHING);
  ELSE {
    PRINT_STRING(CARRYING);
    WHILE (I < NUM_CARRIED) {
      IF (I > 0) MON_CROUT();
      POSITION_CURSOR(18,MON_CV[0,1]);
      PRINT_OBJ_NAME((@INVENTORY)[I,1]);    
      INCR I;
    }
  }  
}