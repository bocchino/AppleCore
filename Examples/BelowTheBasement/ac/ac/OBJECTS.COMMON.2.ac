# -----------------------------------
# GET CURRENT OBJECT MODULE
# -----------------------------------
FN:1 GET_OBJ_MODULE() {
  RETURN OBJ_MODULE[0,1];
}
# -----------------------------------
# FIND OBJECT BY NAME
# -----------------------------------
FN:1 FIND_OBJ(OBJ:2) {
  VAR OBJ_ID:1;
  # LOOK IN ROOM
  SET OBJ_ID=
    FIND_OBJ_IN_RM(OBJ);
  # OTHERWISE LOOK IN INVENTORY
  IF (OBJ_ID=OBJ_NONE)
    SET OBJ_ID=FIND_OBJ_IN_INV(OBJ);
  ELSE
    SET INV_IDX=OBJ_NONE;
  RETURN OBJ_ID;
}
# -----------------------------------
# FIND OBJECT BY ID
# -----------------------------------
FN:1 FIND_OBJ_ID(OBJ_ID:1) {
  # LOOK IN ROOM
  IF (FIND_OBJ_ID_IN_RM(OBJ_ID))
    RETURN TRUE;
  # OTHERWISE LOOK IN INVENTORY
  RETURN FIND_OBJ_ID_IN_INV(OBJ_ID);
}
# -----------------------------------
# DELETE OBJECT FROM GAME
# -----------------------------------
FN:1 REMOVE_OBJ_ID(OBJ_ID:1) {
  IF (FIND_OBJ_ID_IN_RM(OBJ_ID)) {
    REMOVE_OBJ_FROM_RM();
    RETURN TRUE;
  }
  IF (FIND_OBJ_ID_IN_INV(OBJ_ID)) {
    REMOVE_FROM_INV();
    RETURN TRUE;
  }
  RETURN FALSE;
}
# -----------------------------------
# PRINT OBJECT NAME
# -----------------------------------
FN PRINT_OBJ_NAME(OBJ:1) {
  VAR NAME_1:2=OBJ_NAMES[4*OBJ,2];
  VAR NAME_2:2=OBJ_NAMES[4*OBJ+2,2];
  IF (NAME_1[0,1]>0) {
    PRINT_STRING(NAME_1);
    PRINT_CHAR(' ');
    IF (NOT CMP_STRING(NAME_1,WORD_SLIP)) {
      PRINT_STRING(WORD_OF);
      PRINT_CHAR(' ');
    }
  }
  PRINT_STRING(NAME_2);
}
# -----------------------------------
# TEST OBJECT NAME AGAINST ID
# -----------------------------------
FN:1 TEST_OBJ_NAME(STR:2,ID:1) {
  VAR NAME_PTR:2=OBJ_NAMES+4*ID;
  RETURN (NOT CMP_STRING(STR,NAME_PTR[0,2]))
    OR (NOT CMP_STRING(STR,NAME_PTR[2,2]));    
}
# -----------------------------------
# IS THERE A WEAPON?
# -----------------------------------
DATA NO_WEAPON "YOU DON'T HAVE A WEAPON.";
FN:1 HAVE_WEAPON() {
  IF (FIND_OBJ_ID_IN_INV(OBJ_KNIFE))
    RETURN TRUE;
  RESPOND(NO_WEAPON);
  RETURN FALSE;
}  
