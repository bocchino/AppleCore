# -----------------------------------
# BELOW THE BASEMENT: OBJECT MODULE 0
# -----------------------------------
# MODULE NUMBER
# -----------------------------------
DATA OBJ_MODULE    0;
# -----------------------------------
# OBJECT ACTIONS
# -----------------------------------
DATA OBJ_ACTIONS  STOOL_ACTION;
DATA              SWITCH_ACTION;
DATA		  OUTLET_ACTION;
DATA		  CLOSET_ACTION;
DATA		  KEY_ACTION;
# -----------------------------------
# OBJECT ACTION DATA
# -----------------------------------
DATA CLIMB_STOOL "YOU CLIMB THE STEP STOOL TO NOTICE THAT "\;
DATA             "NOW YOU CAN JUST REACH THAT ELECTRIC SWITCH.";

FN STOOL_ACTION() {
  IF (VB_ID=VB_USE) {
    RESPOND(CLIMB_STOOL);
    SET USED_STOOL=TRUE;
  }
  CANT_TAKE(TOO_HEAVY);
}

DATA TOO_HIGH "THE SWITCH IS TOO HIGH TO REACH.";
DATA BUZZING  "WITH A BUZZING NOISE THE PANEL SLIDES BACK "\;
DATA          "REVEALING A DIMLY LIT STAIRCASE DESCENDING INTO THE UNKNOWN.";
DATA CLICK    "CLICK.";

FN SWITCH_ACTION() {
  IF (VB_ID=VB_USE) {
    IF (NOT USED_STOOL) 
      RESPOND(TOO_HIGH);
    ELSE IF (NOT USED_SWITCH) {
      RESPOND(BUZZING);
      SET USED_SWITCH=TRUE;
    }
    ELSE RESPOND(CLICK);
  }
  CANT_TAKE(FIXED);
}

FN OUTLET_ACTION() {
  CANT_TAKE(FIXED);
}

DATA LOOSE "YOU TURN THE KNOB, BUT NOTHING HAPPENS. "\;
DATA 	   "IT SEEMS THERE IS A SCREW LOOSE.";

DATA OPEN_CLOSET "YOU TURN THE KNOB, AND THIS TIME IT RELEASES THE LATCH. "\;
DATA 		 "YOU OPEN THE CLOSET TO REVEAL A CAN OF OIL.";

FN CLOSET_ACTION() {
  IF (VB_ID=VB_OPEN) {
    IF (NOT SCREW_TIGHTENED)
      RESPOND(LOOSE);
    ELSE IF (NOT CLOSET_OPEN) {
      RESPOND(OPEN_CLOSET);    
      SET CLOSET_OPEN=TRUE;
      SET OBJ_ID=OBJ_OIL;
      ADD_OBJ_TO_RM(TRUE);
    }    
  }
  CANT_TAKE(FIXED);
}

DATA KEY_FITS "THE KEY FITS, AND WITH A TURN OF THE WRIST, "\;
DATA 	      "THE DOOR CLICKS UNLOCKED.";

FN KEY_ACTION() {
  IF (CURRENT_RM=RM_FOYER AND
   FOYER_LOCKED) {
    RESPOND(KEY_FITS);
    SET FOYER_LOCKED=FALSE;
  }
  TAKE_ACTION();
}