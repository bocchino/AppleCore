# -----------------------------------
# BELOW THE BASEMENT: VERBS
# -----------------------------------
# VERB CODES
# -----------------------------------
CONST NUM_VBS 11;

CONST VB_LOOK    0;
CONST VB_SEARCH  1;
CONST VB_EXAMINE 2;
CONST VB_INSPECT 3;
CONST VB_TAKE    4;
CONST VB_GET     5;
CONST VB_DROP    6;
CONST VB_USE     7;
CONST VB_SAY     8;
CONST VB_KILL    9;
CONST VB_OPEN    10;

DATA VB_NAMES VB_LOOK_NAME;
DATA          VB_SEARCH_NAME;
DATA          VB_EXAMINE_NAME;
DATA          VB_INSPECT_NAME;
DATA          VB_TAKE_NAME;
DATA          VB_GET_NAME;
DATA	      VB_DROP_NAME;
DATA          VB_USE_NAME;
DATA          VB_SAY_NAME;
DATA          VB_KILL_NAME;
DATA          VB_OPEN_NAME;
  
DATA VB_LOOK_NAME    "LOOK";
DATA VB_SEARCH_NAME  "SEARCH";
DATA VB_EXAMINE_NAME "EXAMINE";
DATA VB_INSPECT_NAME "INSPECT";
DATA VB_TAKE_NAME    "TAKE";
DATA VB_GET_NAME     "GET";
DATA VB_DROP_NAME    "DROP";
DATA VB_USE_NAME     "USE";
DATA VB_SAY_NAME     "SAY";
DATA VB_KILL_NAME    "KILL";
DATA VB_OPEN_NAME    "OPEN";

DATA NOTHING_SPECIAL "THERE IS NOTHING SPECIAL ABOUT THE ";
DATA CANT_DO         "YOU CAN'T DO THAT RIGHT NOW.";
DATA WONT_HELP       "THAT WON'T HELP YOU.";
DATA STR_DONT_US     "I DON'T UNDERSTAND ";

VAR VB:2;
VAR OBJ:2;

VAR VB_ID:1;
VAR OBJ_ID:1;

VAR RESPONDED:1=FALSE;

FN DO_VERB_PHRASE() {
  VAR TARGET_OBJ:1;
  SET VB=(@WORDS)[0,2];
  SET OBJ=(@WORDS)[2,2];
  MON_CROUT();
  SET VB_ID=FIND_WORD(VB,
    VB_NAMES,NUM_VBS);
  IF (VB_ID=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(VB);
    PRINT_CHAR('.');
  }
  ELSE IF (VB_ID=VB_SAY)
    VB_SAY_FN();
  ELSE {
    SET RESPONDED=FALSE;
    SET OBJ_ID=FIND_OBJ(OBJ);
    IF (NOT (OBJ_ID=OBJ_NONE))
      DO_ACTION_ON(OBJ_ID);
    IF (NOT RESPONDED) {
      IF (VB_ID=VB_LOOK AND 
       NOT (OBJ_ID=OBJ_NONE)) {
	PRINT_WORDS(NOTHING_SPECIAL);
	PRINT_WORDS(OBJ_NAMES[4*OBJ_ID+2,2]);
	PRINT_CHAR('.');
      }
      ELSE PRINT_WORDS(CANT_DO);
    }
  }
}

DATA OK "OK";

FN VB_SAY_FN() {
  PRINT_STRING(OK);
}