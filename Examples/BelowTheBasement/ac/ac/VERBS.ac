# -----------------------------------
# BELOW THE BASEMENT: VERBS
# -----------------------------------
# VERB CODES
# -----------------------------------
CONST NUM_VBS 11;

CONST VB_LOOK    0;
CONST VB_SEARCH  1;
CONST VB_EXAMINE 2;
CONST VB_INSPECT 3;
CONST VB_TAKE    4;
CONST VB_GET     5;
CONST VB_DROP    6;
CONST VB_USE     7;
CONST VB_SAY     8;
CONST VB_KILL    9;
CONST VB_OPEN    10;

DATA VB_NAMES VB_LOOK_NAME;
DATA          VB_SEARCH_NAME;
DATA          VB_EXAMINE_NAME;
DATA          VB_INSPECT_NAME;
DATA          VB_TAKE_NAME;
DATA          VB_GET_NAME;
DATA	      VB_DROP_NAME;
DATA          VB_USE_NAME;
DATA          VB_SAY_NAME;
DATA          VB_KILL_NAME;
DATA          VB_OPEN_NAME;
  
DATA VB_LOOK_NAME    "LOOK";
DATA VB_SEARCH_NAME  "SEARCH";
DATA VB_EXAMINE_NAME "EXAMINE";
DATA VB_INSPECT_NAME "INSPECT";
DATA VB_TAKE_NAME    "TAKE";
DATA VB_GET_NAME     "GET";
DATA VB_DROP_NAME    "DROP";
DATA VB_USE_NAME     "USE";
DATA VB_SAY_NAME     "SAY";
DATA VB_KILL_NAME    "KILL";
DATA VB_OPEN_NAME    "OPEN";

DATA DONT_SEE "I DON'T SEE ANY ";
DATA HERE     " HERE.";
DATA STR_DONT_US "I DON'T UNDERSTAND ";
DATA STR_USE  "USE";
DATA NONSENSE "THAT DOESN'T MAKE SENSE.";

VAR VB_STR:2;
VAR O1_STR:2;
VAR O2_STR:2;

VAR VB_ID:1;
VAR OBJ_1_ID:1;
VAR OBJ_2_ID:1;

VAR SUCCESS:1=FALSE;
DATA NOTHING_SPECIAL "THERE IS NOTHING SPECIAL ABOUT THE ";
DATA CANT_DO         "YOU CAN'T DO THAT RIGHT NOW.";

FN DO_VERB_PHRASE() {
  VAR TARGET_OBJ:1;
  SET VB_STR=(@WORDS)[0,2];
  SET O1_STR=(@WORDS)[2,2];
  SET O2_STR=(@WORDS)[4,2];
  SET SUCCESS=FALSE;
  MON_CROUT();
  SET VB_ID=FIND_WORD(VB_STR,
    VB_NAMES,NUM_VBS);
  SET OBJ_1_ID=FIND_WORD(O1_STR,
    OBJ_NAMES,2*NUM_OBJS);
  SET OBJ_2_ID=FIND_WORD(O2_STR,
    OBJ_NAMES,2*NUM_OBJS);
  IF (VB_ID=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(VB_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (VB_ID=VB_SAY)
    VB_SAY_FN();
  ELSE IF (OBJ_1_ID=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(O1_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (NUM_WORDS > 2 AND
    NOT ((OBJ_1_ID AND 1)=0 AND
     OBJ_2_ID=OBJ_1_ID+1))
      PRINT_WORDS(NONSENSE);
  ELSE {
    SET TARGET_OBJ=OBJ_1_ID>>1;
    IF (NOT IS_HERE(TARGET_OBJ,
     CURRENT_RM)) {
      PRINT_WORDS(DONT_SEE);
      PRINT_WORDS(O1_STR);
      PRINT_WORDS(HERE);
    }
    ELSE {
      DO_ACTION_ON(TARGET_OBJ);
      IF (NOT SUCCESS) {
        IF (VB_ID=VB_LOOK) {
	  PRINT_WORDS(NOTHING_SPECIAL);
	  PRINT_WORDS(OBJ_NAMES[4*TARGET_OBJ+2,2]);
	  PRINT_CHAR('.');
	}
	ELSE
	  PRINT_WORDS(CANT_DO);
      }
    }
  }
}

DATA OK "OK";

FN VB_SAY_FN() {
  PRINT_STRING(OK);
}