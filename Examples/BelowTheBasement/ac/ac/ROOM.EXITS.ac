# -----------------------------------
# BELOW THE BASEMENT: EXIT HANDLERS
# -----------------------------------
# GO TO DIRECTION
# -----------------------------------
FN GO(TO:1) {
  VAR EXIT:1=RM_EXIT(CURRENT_RM,TO);
  IF (CURRENT_RM=RM_RESERVOIR)
    EXIT_RESERVOIR(TO,EXIT);
  ELSE IF (CURRENT_RM=RM_BANK)
    EXIT_BANK(TO,EXIT);
  ELSE IF (CURRENT_RM=RM_FOYER)
    EXIT_FOYER(TO,EXIT);
  ELSE GO_TO_RM(EXIT);
}
# -----------------------------------
# GO TO ROOM
# -----------------------------------
FN GO_TO_RM(RM:1) {
  IF (RM=RM_NONE)
    PRINT_WORDS(CANT_GO);
  ELSE {
    SET CURRENT_RM=RM;
    CMD_L_FN();
  }
}
# -----------------------------------
# EXIT THE RESERVOIR
# -----------------------------------
FN EXIT_RESERVOIR(TO:1,EXIT:1) {
  IF (TO=NORTH)
    IF (NOT HANDLE_CANOE())
      RETURN;
  GO_TO_RM(EXIT);
}
# -----------------------------------
# EXIT THE BANK
# -----------------------------------
FN EXIT_BANK(TO:1,EXIT:1) {
  IF (TO=SOUTH)
    IF (NOT HANDLE_CANOE())
      RETURN;
  GO_TO_RM(EXIT);
}    
# -----------------------------------
# HANDLE THE CANOE
# -----------------------------------
DATA CANOE_HOLE "WHOOPS, THERE IS A HOLE IN YOUR BOAT!  YOU QUICKLY TAKE ON WATER AND SINK.  "\;
DATA            "YOU STRUGGLE BACK TO SHORE, BUT YOU ARE NOT GOING ANYWHERE TILL THAT HOLE IS PLUGGED.";

DATA NO_PADDLE "NOW YOU'RE UP A CREEK WITHOUT A PADDLE!  "\;
DATA 	       "THE CURRENT IS TAKING YOU DOWNSTREAM.  "\;
DATA	       "YOU SEE SOMETHING...IT'S A HUGE ROCK!  "\;
DATA	       "I'M SORRY, BUT THIS IS THE END.";

DATA CANOEING  "YOU HOP INTO THE CANOE AND PADDLE TO THE OPPOSITE SHORE.";

FN:1 HANDLE_CANOE() {
  VAR RESULT:1=FALSE;
  IF (NOT FIND_OBJ_ID_IN_INV(OBJ_CANOE))
    PRINT_WORDS(CANT_GO);
  ELSE IF (NOT CANOE_PLUGGED)
    PRINT_WORDS(CANOE_HOLE);    
  ELSE IF (NOT FIND_OBJ_ID_IN_INV(OBJ_PADDLE)) {
    PRINT_WORDS(NO_PADDLE);
    SET DONE=TRUE;
  }
  ELSE {
    PRINT_WORDS(CANOEING);
    MON_RDKEY(); 
    SET RESULT=TRUE;
  } 
  RETURN RESULT;
}
# -----------------------------------
# EXIT THE FOYER
# -----------------------------------
DATA DOOR_LOCKED "THE DOOR IS LOCKED.";
DATA DOOR_HELD   "THE DOOR WON'T OPEN.";
DATA DOOR_SWINGS "THE DOOR SWINGS OPEN TO REVEAL A STAIRCASE LEADING DOWN.";

FN EXIT_FOYER(TO:1,EXIT:1) {
  IF (TO=NORTH) {
    IF (FOYER_LOCKED)
      PRINT_WORDS(DOOR_LOCKED);
    ELSE IF (FOYER_HELD)
      PRINT_WORDS(DOOR_HELD);
    ELSE
      PRINT_WORDS(DOOR_SWINGS);
  }
  ELSE IF (TO=DOWN 
   AND NOT FOYER_LOCKED
   AND NOT FOYER_HELD) {
    GO_TO_RM(RM_DARK);
  }
  ELSE GO_TO_RM(EXIT);
}

