# -----------------------------------
# MINI ADVENTURE: VERBS
# -----------------------------------
# VERB CODES
# -----------------------------------
CONST NUM_VBS 8;

CONST VB_TAKE    0;
CONST VB_GET     1;
CONST VB_LOOK    2;
CONST VB_SEARCH  3;
CONST VB_EXAMINE 4;
CONST VB_DROP    5;
CONST VB_UNBOLT  6;
CONST VB_OPEN    7;

DATA VB_NAMES VB_TAKE_NAME;
DATA          VB_GET_NAME;
DATA          VB_LOOK_NAME;
DATA          VB_SEARCH_NAME;
DATA          VB_EXAMINE_NAME;
DATA          VB_DROP_NAME;
DATA          VB_UNBOLT_NAME;
DATA          VB_OPEN_NAME;
  
DATA VB_TAKE_NAME    "TAKE";
DATA VB_GET_NAME     "GET";
DATA VB_LOOK_NAME    "LOOK";
DATA VB_SEARCH_NAME  "SEARCH";
DATA VB_EXAMINE_NAME "EXAMINE";
DATA VB_DROP_NAME    "DROP";
DATA VB_UNBOLT_NAME  "UNBOLT";
DATA VB_OPEN_NAME    "OPEN";

DATA VB_FNS  VB_TAKE_FN;
DATA         VB_TAKE_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_DROP_FN;
DATA         VB_UNBOLT_FN;
DATA         VB_OPEN_FN;

DATA DONT_SEE "I DON'T SEE ANY ";
DATA HERE     " HERE.";
DATA STR_DONT_US "I DON'T UNDERSTAND ";
DATA STR_USE  "USE";
DATA MORE_SPECIFIC "YOU'LL HAVE TO BE MORE SPECIFIC.";

FN DO_VERB_PHRASE() {
  VAR VB_STR:2=(@WORDS)[0,2];
  VAR O1_STR:2=(@WORDS)[2,2];
  VAR O2_STR:2=(@WORDS)[4,2];
  VAR VB:1;
  MON_CROUT();
  IF (NOT CMP_STRING(VB_STR,STR_USE)) {
    # "USE X TO Y Z"=>"Y Z WITH X"
    IF (NUM_WORDS=4) {
      SET VB_STR=(@WORDS)[4,2];
      SET O1_STR=(@WORDS)[6,2];
      SET O2_STR=(@WORDS)[2,2];
    }
    ELSE {
      PRINT_WORDS(MORE_SPECIFIC);
      MON_CROUT();
      RETURN;
    }
  }
  SET VB=FIND_WORD(VB_STR,
    VB_NAMES,NUM_VBS);
  SET FOCUS_OBJ=FIND_WORD(O1_STR,
    OBJ_NAMES,NUM_OBJS);
  SET SECOND_OBJ=FIND_WORD(O2_STR,
    OBJ_NAMES,NUM_OBJS);
  IF (VB=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(VB_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (FOCUS_OBJ=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(O1_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (NOT IS_HERE(FOCUS_OBJ,
   CURRENT_RM)) {
    PRINT_WORDS(DONT_SEE);
    PRINT_WORDS(O1_STR);
    PRINT_WORDS(HERE);
  }
  ELSE {
    VB_FNS[VB*2,2]();
  }
  MON_CROUT();
}
# -----------------------------------
# VERB HANDLERS
# -----------------------------------
DATA HAVE_STR "YOU ALREADY HAVE IT.";

FN VB_TAKE_FN() {
  IF (OBJ_RM(FOCUS_OBJ)=RM_TAKEN) {
    PRINT_WORDS(HAVE_STR);
  }
  ELSE {
    OBJ_TAKE_FN(FOCUS_OBJ)();
  }
}

FN VB_LOOK_FN() {
  OBJ_DESC_FN(FOCUS_OBJ)();
}

DATA DONT_HAVE "YOU DON'T HAVE IT.";
DATA DROPPED   "DROPPED";

FN VB_DROP_FN() {
  VAR I:1=0;
  IF (IS_CARRIED(FOCUS_OBJ)) {
    SET (OBJ_RM_PTR(FOCUS_OBJ))[0,1]=
     (CURRENT_RM OR DROPPED_FLAG);
    WHILE (I < NUM_CARRIED) {
      IF (CARRIED_OBJ(I)=FOCUS_OBJ) {
        DECR NUM_CARRIED;
        SET_CARRIED(I,CARRIED_OBJ(NUM_CARRIED));
      }
      INCR I;
    }
    PRINT_STRING(DROPPED);
  }
  ELSE {
    PRINT_WORDS(DONT_HAVE);
  }  
}

DATA DOESNT_WORK "THAT DOESN'T WORK.";

DATA UNBOLT "YOU LOOSEN THE BOLTS WITH THE WRENCH.";
DATA ALREADY_UNBOLTED "THE GRATE IS ALREADY UNBOLTED.";
DATA NEED_WRENCH "YOU NEED A WRENCH.";

FN VB_UNBOLT_FN() {
  IF ((FOCUS_OBJ=OBJ_GRATE) OR
   (FOCUS_OBJ=OBJ_BOLTS)) {
     IF (NOT (OBJ_TAKE_FN(OBJ_GRATE)=
      OBJ_TAKE_BOLTED)) {
       PRINT_WORDS(ALREADY_UNBOLTED);
     }
     ELSE IF (NOT IS_CARRIED(OBJ_WRENCH)) {
       PRINT_WORDS(NEED_WRENCH);
     }
     ELSE {     
       PRINT_WORDS(UNBOLT);
       SET (OBJ_TAKE_PTR(OBJ_GRATE))[0,2]=
        OBJ_TAKE_TOO_HEAVY;
       SET (OBJ_TAKE_PTR(OBJ_BOLTS))[0,2]=
        OBJ_TAKE_OK;
       SET (OBJ_RM_PTR(OBJ_BOLTS))[0,1]=
        (RM_ENTER OR DROPPED_FLAG);
     }
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA OPEN_GRATE "YOU OPEN THE GRATE, REVEALING A PASSAGE DOWN TO A ROOM BELOW.";

FN VB_OPEN_FN() {
  IF (FOCUS_OBJ=OBJ_GRATE) {
    IF (OBJ_TAKE_FN(OBJ_GRATE)=
     OBJ_TAKE_TOO_HEAVY) {
      PRINT_WORDS(OPEN_GRATE);
      SET (RM_EXIT_PTR(RM_ENTER,DOWN))[0,1]=
       RM_GRIMY;      
    }
    ELSE {
      OBJ_TAKE_BOLTED();
    }
  }
  ELSE {

  }
}