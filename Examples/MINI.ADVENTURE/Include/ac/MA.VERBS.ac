# -----------------------------------
# MINI ADVENTURE: VERBS
# -----------------------------------
# VERB CODES
# -----------------------------------
CONST NUM_VBS 11;

CONST VB_TAKE    0;
CONST VB_GET     1;
CONST VB_LOOK    2;
CONST VB_SEARCH  3;
CONST VB_EXAMINE 4;
CONST VB_DROP    5;
CONST VB_UNBOLT  6;
CONST VB_LOOSEN  7;
CONST VB_OPEN    8;
CONST VB_SAY     9;
CONST VB_TURN    10;

DATA VB_NAMES VB_TAKE_NAME;
DATA          VB_GET_NAME;
DATA          VB_LOOK_NAME;
DATA          VB_SEARCH_NAME;
DATA          VB_EXAMINE_NAME;
DATA          VB_DROP_NAME;
DATA          VB_UNBOLT_NAME;
DATA          VB_LOOSEN_NAME;
DATA          VB_OPEN_NAME;
DATA          VB_SAY_NAME;
DATA          VB_TURN_NAME;
  
DATA VB_TAKE_NAME    "TAKE";
DATA VB_GET_NAME     "GET";
DATA VB_LOOK_NAME    "LOOK";
DATA VB_SEARCH_NAME  "SEARCH";
DATA VB_EXAMINE_NAME "EXAMINE";
DATA VB_DROP_NAME    "DROP";
DATA VB_UNBOLT_NAME  "UNBOLT";
DATA VB_LOOSEN_NAME  "LOOSEN";
DATA VB_OPEN_NAME    "OPEN";
DATA VB_SAY_NAME     "SAY";
DATA VB_TURN_NAME    "TURN";

DATA VB_FNS  VB_TAKE_FN;
DATA         VB_TAKE_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_DROP_FN;
DATA         VB_UNBOLT_FN;
DATA         VB_LOOSEN_FN;
DATA         VB_OPEN_FN;
DATA         VB_SAY_FN;
DATA         VB_TURN_FN;

DATA DONT_SEE "I DON'T SEE ANY ";
DATA HERE     " HERE.";
DATA STR_DONT_US "I DON'T UNDERSTAND ";
DATA STR_USE  "USE";
DATA MORE_SPECIFIC "YOU'LL HAVE TO BE MORE SPECIFIC.";

VAR VB_STR:2;
VAR O1_STR:2;
VAR O2_STR:2;

FN DO_VERB_PHRASE() {
  VAR VB:1;  
  SET VB_STR=(@WORDS)[0,2];
  SET O1_STR=(@WORDS)[2,2];
  SET O2_STR=(@WORDS)[4,2];
  MON_CROUT();
  IF (NOT CMP_STRING(VB_STR,STR_USE)) {
    # "USE X TO Y Z"=>"Y Z WITH X"
    IF (NUM_WORDS=4) {
      SET VB_STR=(@WORDS)[4,2];
      SET O1_STR=(@WORDS)[6,2];
      SET O2_STR=(@WORDS)[2,2];
    }
    ELSE {
      PRINT_WORDS(MORE_SPECIFIC);
      MON_CROUT();
      RETURN;
    }
  }
  SET VB=FIND_WORD(VB_STR,
    VB_NAMES,NUM_VBS);
  SET FOCUS_OBJ=FIND_WORD(O1_STR,
    OBJ_NAMES,NUM_OBJS);
  SET SECOND_OBJ=FIND_WORD(O2_STR,
    OBJ_NAMES,NUM_OBJS);
  IF (VB=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(VB_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (VB=VB_SAY) {
    VB_SAY_FN();
  }
  ELSE IF (FOCUS_OBJ=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(O1_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (NOT IS_HERE(FOCUS_OBJ,
   CURRENT_RM)) {
    PRINT_WORDS(DONT_SEE);
    PRINT_WORDS(O1_STR);
    PRINT_WORDS(HERE);
  }
  ELSE {
    VB_FNS[VB*2,2]();
  }
  MON_CROUT();
}
