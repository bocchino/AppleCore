# -----------------------------------
# MINI ADVENTURE: VERBS
# -----------------------------------
# VERB CODES
# -----------------------------------
CONST NUM_VBS 6;

CONST VB_TAKE    0;
CONST VB_GET     1;
CONST VB_LOOK    2;
CONST VB_SEARCH  3;
CONST VB_EXAMINE 4;
CONST VB_DROP    5;

DATA VB_NAMES VB_TAKE_NAME;
DATA          VB_GET_NAME;
DATA          VB_LOOK_NAME;
DATA          VB_SEARCH_NAME;
DATA          VB_EXAMINE_NAME;
DATA          VB_DROP_NAME;
  
DATA VB_TAKE_NAME    "TAKE";
DATA VB_GET_NAME     "GET";
DATA VB_LOOK_NAME    "LOOK";
DATA VB_SEARCH_NAME  "SEARCH";
DATA VB_EXAMINE_NAME "EXAMINE";
DATA VB_DROP_NAME    "DROP";

DATA VB_FNS  VB_TAKE_FN;
DATA         VB_TAKE_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_LOOK_FN;
DATA         VB_DROP_FN;

DATA DONT_SEE "I DON'T SEE ANY ";
DATA HERE     " HERE.";
DATA STR_DONT_US "I DON'T UNDERSTAND ";

FN DO_VERB_PHRASE() {
  VAR VB_STR:2=(@WORDS)[0,2];
  VAR O1_STR:2=(@WORDS)[2,2];
  VAR O2_STR:2=(@WORDS)[4,2];
  VAR VB:1=FIND_WORD(VB_STR,
    VB_NAMES,NUM_VBS);
  SET FOCUS_OBJ=FIND_WORD(O1_STR,
    OBJ_NAMES,NUM_OBJS);
  SET SECOND_OBJ=FIND_WORD(O2_STR,
    OBJ_NAMES,NUM_OBJS);
  MON_CROUT();
  IF (VB=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(VB_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (FOCUS_OBJ=-1) {
    PRINT_WORDS(STR_DONT_US);
    PRINT_WORDS(O1_STR);
    PRINT_CHAR('.');
  }
  ELSE IF (NOT IS_HERE(FOCUS_OBJ,
   CURRENT_RM)) {
    PRINT_WORDS(DONT_SEE);
    PRINT_WORDS(O1_STR);
    PRINT_WORDS(HERE);
  }
  ELSE {
    VB_FNS[VB*2,2]();
  }
  MON_CROUT();
}
# -----------------------------------
# VERB HANDLERS
# -----------------------------------
DATA HAVE_STR "YOU ALREADY HAVE IT.";

FN VB_TAKE_FN() {
  IF (OBJ_RM(FOCUS_OBJ)=RM_TAKEN) {
    PRINT_WORDS(HAVE_STR);
  }
  ELSE {
    OBJ_TAKE_FN(FOCUS_OBJ)();
  }
}

FN VB_LOOK_FN() {
  OBJ_DESC_FN(FOCUS_OBJ)();
}

DATA DONT_HAVE "YOU DON'T HAVE ANY ";
DATA DROPPED   "DROPPED";

FN VB_DROP_FN() {
  VAR I:1=0;
  IF (IS_CARRIED(FOCUS_OBJ)) {
    SET (OBJ_RM_PTR(FOCUS_OBJ))[0,1]=
     (CURRENT_RM OR DROPPED_FLAG);
    WHILE (I < NUM_CARRIED) {
      IF (CARRIED_OBJ(I)=FOCUS_OBJ) {
        DECR NUM_CARRIED;
        SET_CARRIED(I,CARRIED_OBJ(NUM_CARRIED));
      }
      INCR I;
    }
    PRINT_STRING(DROPPED);
  }
  ELSE {
    PRINT_WORDS(DONT_HAVE);
    PRINT_STRING(FOCUS_OBJ);
    PRINT_CHAR('.');
  }  
}