# -----------------------------------
# MINI ADVENTURE: VERBS, PART 3
# -----------------------------------
DATA WONT_HELP "THAT WON'T HELP YOU.";
DATA WONT_WORK "THAT WON'T WORK.";
DATA IDIOT     "YOU TRY TO SQUASH THE SPIDERS WITH YOUR HAND, BUT THEY SCURRY FARTHER UP INTO THE MAZE OF WEBS.  "\;
DATA           "YOU HEAR A VAGUE MUTTERING THAT SOUNDS SOMETHING LIKE \$22IDIOT!\$22";
DATA ALREADY_DEAD "THE OTYUGH IS ALREADY DEAD.";
DATA BARE_HANDS "WITH YOUR BARE HANDS?";
DATA NO_KNIFE   "YOU DON'T HAVE A KNIFE!";
DATA KILL_OTY  "YOU SLASH ONE OF ITS TENTACLES AND IT CRIES OUT IN RAGE AND PAIN.  YOU THEN DRIVE THE KNIFE INTO ITS GUTS AND FINISH IT OFF.";
DATA WOUND_OTY "YOU GASH ITS LEGS, BUT A MEAN SWIPE OF ITS TENTACLES ALMOST TAKES YOUR HEAD OFF.";

VAR ATTACKED_OT:1=0;

# RANDOM NUMBER SEEDED FROM KEYBOARD POLLING
CONST RNDL $4E;

FN VB_KILL_FN() {
  IF (FOCUS_OBJ=OBJ_SPIDERS AND CURRENT_RM=RM_GRIMY) {
    PRINT_WORDS(IDIOT);
  }
  ELSE IF (FOCUS_OBJ=OBJ_OTYUGH) {
    IF (NOT OTYUGH_ALIVE) {
      PRINT_WORDS(ALREADY_DEAD);
    }
    ELSE IF (SECOND_OBJ=OBJ_NONE) {
      PRINT_WORDS(BARE_HANDS);
    }
    ELSE IF (NOT (SECOND_OBJ=OBJ_KNIFE)) {
      PRINT_WORDS(WONT_WORK);
    }
    ELSE IF (NOT IS_HERE(OBJ_KNIFE,CURRENT_RM)) {
      PRINT_WORDS(NO_KNIFE);
    }
    ELSE IF (RNDL[0,1]) {
      PRINT_WORDS(WOUND_OTY);
      SET OT_TURNS=1;
    }
    ELSE {
      PRINT_WORDS(KILL_OTY);
      SET OTYUGH_ALIVE=FALSE;
    }
  }
  ELSE {
    PRINT_WORDS(WONT_HELP);
  }
}

DATA SEVEN_MAIDS "\$22IF SEVEN MAIDS WITH SEVEN MOPS SWEPT IT FOR HALF A YEAR, "\;
DATA             "DO YOU SUPPOSE,\$22 THE WALRUS SAID, \$22THAT THEY COULD GET IT CLEAR?\$22  "\;
DATA             "\$0D\$0D"\;
DATA             "\$22I DOUBT IT,\$22 SAID THE CARPENTER, AND SHED A BITTER TEAR.";

DATA NO_SCROLL "I DON'T SEE ANY SCROLL HERE.";

FN VB_CLEAN_FN() {
  IF (SECOND_OBJ=OBJ_SCROLL) {
    IF (NOT IS_HERE(OBJ_SCROLL,CURRENT_RM)) {
      PRINT_WORDS(NO_SCROLL);
    }
    ELSE USE_SCROLL();
  }
  ELSE IF (FOCUS_OBJ=OBJ_GARBAGE) {
    PRINT_WORDS(SEVEN_MAIDS);
  }
  ELSE {
    PRINT_WORDS(WONT_HELP);
  }
} 

DATA GLOW "YOU READ THE MAGICAL INCANTATIONS, AND THEY GLOW AND WRITHE ON THE PAGE.  ";
DATA GARBAGE_GONE "THE GARBAGE DISAPPEARS TO DISCLOSE A PLASTER HAND.";
DATA POWERFUL "THE MAGIC IS POWERFUL AND MAKES THE ";
DATA SPARKLING " SPARKLING CLEAN.";
DATA DUST "\$0D\$0DTHE WRITING DISAPPEARS, AND THE SCROLL TURNS TO DUST.";

FN USE_SCROLL() {
  PRINT_WORDS(GLOW);
  IF (FOCUS_OBJ=OBJ_GARBAGE) {
    PRINT_WORDS(GARBAGE_GONE);
    SET OBJ_RM_PTR(OBJ_GARBAGE)[0,1]=RM_NONE;
    SET OBJ_RM_PTR(OBJ_HAND)[0,1]=
      RM_OFFAL OR DROPPED_FLAG;
  }  
  ELSE {
    PRINT_WORDS(POWERFUL);
    PRINT_WORDS(O1_STR);
    PRINT_WORDS(SPARKLING);
  }
  PRINT_WORDS(DUST);
  IF (IS_CARRIED(OBJ_SCROLL)) {
    DROP_ITEM(OBJ_SCROLL);
  }
  SET OBJ_RM_PTR(OBJ_SCROLL)[0,1]=
    RM_NONE;
}

