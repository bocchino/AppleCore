# -----------------------------------
# MINI ADVENTURE: PARSING
# -----------------------------------
VAR WORDS:6;
VAR NUM_WORDS:1;

FN PARSE_INPUT() {
  FIND_WORDS();
  IF (NUM_WORDS=0) {
    RETURN;
  }
  ELSE IF (NUM_WORDS=1) {
    DO_COMMAND();
  }
  ELSE {
    DO_VERB_PHRASE();
  }
}

FN SHOW_WORDS() {
  VAR WORD:1=0;
  WHILE (WORD<NUM_WORDS) {
    PRINT_STRING((@WORDS)[WORD*2,2]);
    MON_CROUT();
    INCR WORD;
  }
}
# -----------------------------------
# FIND THE WORDS IN THE INPUT LINE
# -----------------------------------
FN FIND_WORDS() {
  VAR WORD:2=MON_IN;
  VAR PTR:2=MON_IN;
  VAR CHAR:1;
  SET (@WORDS)[0,6]=0;
  SET NUM_WORDS=0;
  WHILE (NUM_WORDS<3) {
    SET CHAR=PTR[0,1];
    IF (EQ_CHAR(CHAR,' ') OR (CHAR=0)) {
      SET PTR[0,1]=0;
      IF (NOT IS_SKIP_WORD(WORD)) {
        SET (@WORDS)[NUM_WORDS*2,2]=WORD;
        INCR NUM_WORDS;
      }
      IF (CHAR=0) RETURN;
      INCR PTR;
      WHILE (EQ_CHAR(PTR[0,1],' ')) {
        INCR PTR;
      }
      SET WORD=PTR;
    }
    ELSE {
      INCR PTR;
    }
  }
}
# -----------------------------------
# WORDS TO SKIP
# -----------------------------------
CONST NUM_SKIP_WORDS 5;

DATA WORD_PTRS WORD_AT;
DATA           WORD_WITH;
DATA	       WORD_IN;
DATA           WORD_THE;
DATA           WORD_A;

DATA WORD_AT       "AT";
DATA WORD_WITH     "WITH";
DATA WORD_IN       "IN";
DATA WORD_THE      "THE";
DATA WORD_A        "A";

FN:1S IS_SKIP_WORD(STR:2) {
  VAR I:1=0;
  WHILE (I<NUM_SKIP_WORDS) {
    IF (NOT CMP_STRING(STR,WORD_PTRS[I*2,2])) {
      RETURN TRUE;
    }
    INCR I;
  } 
  RETURN FALSE; 
}



