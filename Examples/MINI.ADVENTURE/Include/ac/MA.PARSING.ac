# -----------------------------------
# MINI ADVENTURE: PARSING
# -----------------------------------
VAR WORDS:6;
VAR NUM_WORDS:1;

FN PARSE_INPUT() {
  FIND_WORDS();
  IF (NUM_WORDS=0) {
    RETURN;
  }
  ELSE IF (NUM_WORDS=1) {
    DO_COMMAND();
  }
  ELSE {
    DO_VERB_PHRASE();
  }
}
# -----------------------------------
# FIND THE WORDS IN THE INPUT LINE
# -----------------------------------
FN FIND_WORDS() {
  VAR WORD:2=MON_IN;
  VAR PTR:2=MON_IN;
  VAR CHAR:1;
  SET (@WORDS)[0,6]=0;
  SET NUM_WORDS=0;
  WHILE (NUM_WORDS<3) {
    SET CHAR=PTR[0,1];
    IF (EQ_CHAR(CHAR,' ') OR (CHAR=0)) {
      SET PTR[0,1]=0;
      IF (NOT IS_SKIP_WORD(WORD)) {
        SET (@WORDS)[NUM_WORDS*2,2]=WORD;
        INCR NUM_WORDS;
      }
      IF (CHAR=0) RETURN;
      INCR PTR;
      WHILE (EQ_CHAR(PTR[0,1],' ')) {
        INCR PTR;
      }
      SET WORD=PTR;
    }
    ELSE {
      INCR PTR;
    }
  }
}
# -----------------------------------
# WORDS TO SKIP
# -----------------------------------
CONST NUM_SKIP_WORDS 5;

DATA WORD_PTRS WORD_AT;
DATA           WORD_WITH;
DATA	       WORD_IN;
DATA           WORD_THE;
DATA           WORD_A;

DATA WORD_AT       "AT";
DATA WORD_WITH     "WITH";
DATA WORD_IN       "IN";
DATA WORD_THE      "THE";
DATA WORD_A        "A";

FN:1S IS_SKIP_WORD(STR:2) {
  VAR I:1=0;
  WHILE (I<NUM_SKIP_WORDS) {
    IF (NOT CMP_STRING(STR,WORD_PTRS[I*2,2])) {
      RETURN TRUE;
    }
    INCR I;
  } 
  RETURN FALSE; 
}
# -----------------------------------
# FIND WORD IN LIST
# -----------------------------------
FN:1 FIND_WORD(WORD_PTR:2,
 LIST_PTR:2,MAX:1) {
  VAR IDX:1=0;
  IF (WORD_PTR=0) RETURN -1;
  WHILE (IDX < MAX) {
    IF (NOT CMP_STRING(WORD_PTR,
     LIST_PTR)) {
      RETURN IDX;
    }
    SET LIST_PTR=LIST_PTR+
      STRING_LEN(LIST_PTR)+1;
    INCR IDX;
  }
  RETURN -1;
}

FN:1 FIND_WORD_PTR(WORD_PTR:2,
 LIST_PTR:2,MAX:1) {
  VAR IDX:1=0;
  IF (WORD_PTR=0) RETURN -1;
  WHILE (IDX < MAX) {
    IF (NOT CMP_STRING(WORD_PTR,
     LIST_PTR[IDX*2,2])) {
      RETURN IDX;
    }
    INCR IDX;
  }
  RETURN -1;
}
 

