# -----------------------------------
# MINI ADVENTURE: VERBS, PART 2
# -----------------------------------
DATA HAVE_STR "YOU ALREADY HAVE IT.";

FN VB_TAKE_FN() {
  IF (OBJ_RM(FOCUS_OBJ)=RM_TAKEN) {
    PRINT_WORDS(HAVE_STR);
  }
  ELSE {
    OBJ_TAKE_FN(FOCUS_OBJ)();
  }
}

FN VB_LOOK_FN() {
  OBJ_DESC_FN(FOCUS_OBJ)();
}

DATA DONT_HAVE "YOU DON'T HAVE IT.";
DATA DROPPED   "DROPPED";

FN VB_DROP_FN() {
  IF (IS_CARRIED(FOCUS_OBJ)) {
    DROP_ITEM(FOCUS_OBJ);
    PRINT_STRING(DROPPED);
  }
  ELSE {
    PRINT_WORDS(DONT_HAVE);
  }  
}

FN DROP_ITEM(OBJ:1) {
  VAR I:1=0;
  SET (OBJ_RM_PTR(OBJ))[0,1]=
    (CURRENT_RM OR DROPPED_FLAG);
  WHILE (I < NUM_CARRIED) {
    IF (CARRIED_OBJ(I)=OBJ) {
      DECR NUM_CARRIED;
      SET_CARRIED(I,CARRIED_OBJ(NUM_CARRIED));
    }
    INCR I;
  }
}

DATA DOESNT_WORK "THAT DOESN'T WORK.";

DATA UNBOLT "YOU LOOSEN THE BOLTS WITH THE WRENCH.";
DATA ALREADY_UNBOLTED "THE GRATE IS ALREADY UNBOLTED.";
DATA NEED_WRENCH "YOU NEED A WRENCH.";

FN VB_UNBOLT_FN() {
  IF ((FOCUS_OBJ=OBJ_GRATE) OR
   (FOCUS_OBJ=OBJ_BOLTS)) {
     IF (NOT (OBJ_TAKE_FN(OBJ_GRATE)=
      OBJ_TAKE_BOLTED)) {
       PRINT_WORDS(ALREADY_UNBOLTED);
     }
     ELSE IF (NOT IS_CARRIED(OBJ_WRENCH)) {
       PRINT_WORDS(NEED_WRENCH);
     }
     ELSE {     
       PRINT_WORDS(UNBOLT);
       SET (OBJ_TAKE_PTR(OBJ_GRATE))[0,2]=
        OBJ_TAKE_TOO_HEAVY;
       SET (OBJ_TAKE_PTR(OBJ_BOLTS))[0,2]=
        OBJ_TAKE_OK;
       SET (OBJ_RM_PTR(OBJ_BOLTS))[0,1]=
        (RM_ENTER OR DROPPED_FLAG);
     }
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

FN VB_LOOSEN_FN() {
  IF (FOCUS_OBJ=OBJ_BOLTS) {
    VB_UNBOLT_FN();
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA OPEN_GRATE "YOU OPEN THE GRATE, REVEALING A PASSAGE DOWN TO A ROOM BELOW.";
DATA GRATE_OPEN "THE GRATE IS ALREADY OPEN.";
DATA BARREL_ALREADY "THE BARREL IS ALREADY OPEN.";

DATA WONT_BUDGE "THE LID JUST WON'T BUDGE.";
DATA WONT_OPEN  "IT WON'T COME OPEN.";
DATA HATCH_OPENS "YOU FIT THE PLASTER HAND INTO ITS RECEPTACLE.  THE LARGE HATCH GLOWS CRIMSON AND OPENS.";

FN VB_OPEN_FN() {
  IF (FOCUS_OBJ=OBJ_GRATE) {
    IF (RM_EXIT(RM_ENTER,DOWN)=RM_GRIMY) {
      PRINT_WORDS(GRATE_OPEN);
    }
    ELSE IF (OBJ_TAKE_FN(OBJ_GRATE)=
     OBJ_TAKE_TOO_HEAVY) {
      PRINT_WORDS(OPEN_GRATE);
      SET RM_EXIT_PTR(RM_ENTER,DOWN)[0,1]=
       RM_GRIMY;      
    }
    ELSE OBJ_TAKE_BOLTED();
  }
  ELSE IF (FOCUS_OBJ=OBJ_BARREL) {
    IF (NOT (OBJ_RM(OBJ_SCROLL)=RM_NONE)) {
      PRINT_WORDS(BARREL_ALREADY);
    }
    ELSE PRINT_WORDS(WONT_BUDGE);
  }
  ELSE IF (FOCUS_OBJ=OBJ_HATCH) {
    PRINT_WORDS(HATCH);
  }
  ELSE IF (FOCUS_OBJ=OBJ_SMALL) {
    PRINT_WORDS(WONT_OPEN);
    # TODO: HANDLE CASE WHERE IT'S ALREADY OPEN
  }
  ELSE IF (FOCUS_OBJ=OBJ_LARGE) {
    IF (SECOND_OBJ=OBJ_HAND) {
      PRINT_WORDS(HATCH_OPENS);
      # TODO: ADD WAY OUT
    }
    ELSE PRINT_WORDS(WONT_OPEN);
    # TODO: HANDLE CASE WHERE ALREADY OPEN
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA ZARTAN "ZARTAN";
DATA DULL_THUD "THE BARREL POPS OPEN WITH A DULL THUD.";
DATA NOTHING "NOTHING HAPPENS.";

FN VB_SAY_FN() {
  IF (NOT CMP_STRING(O1_STR,ZARTAN)) {
    PRINT_WORDS(DULL_THUD);
    SET OBJ_RM_PTR(OBJ_SCROLL)[0,1]=RM_GRIMY;
    SET OBJ_RM_PTR(OBJ_TABLET)[0,1]=RM_GRIMY;
  }
  ELSE {
    PRINT_WORDS(NOTHING);
  }
}

DATA ON  "ON";
DATA OFF "OFF";
DATA LAMP_ON_MSG  "THE LAMP IS NOW ON.";
DATA LAMP_OFF_MSG "THE LAMP IS NOW OFF.";

FN VB_TURN_FN() {
  IF (NOT (FOCUS_OBJ=OBJ_LAMP)) {
    PRINT_WORDS(DOESNT_WORK);
  }
  ELSE IF (NOT CMP_STRING(O2_STR,ON)) {
    PRINT_WORDS(LAMP_ON_MSG);
    SET LAMP_ON=TRUE;
  }
  ELSE IF (NOT CMP_STRING(O2_STR,OFF)) {
    PRINT_WORDS(LAMP_OFF_MSG);
    SET LAMP_ON=FALSE;
  }
  ELSE {
    PRINT_WORDS(DOESNT_WORK);
  }
}

DATA READ_TABLET "IT SAYS: I AM ZARTAN THE WIZARD.  "\;
DATA             "I WAS CONDUCTING A TREASURE-FINDING DEMONSTRATION FOR SOME FRIENDS "\;
DATA             "WHEN I FOUND MYSELF ACCIDENTALLY TELEPORTED TO THIS PLACE.  "\;
DATA             "I KNEW THERE WAS GREAT WEALTH HIDDEN WITHIN THESE ROOMS, BUT I WAS UNABLE TO ESCAPE. "\;
DATA             "I FOUND AN IRON GRATE, BUT IT WAS BOLTED FROM THE OUTSIDE."\;
DATA             "\$0D\$0D"\;
DATA             "I RESIGNED MYSELF TO SEARCHING THIS GLOOMY PLACE FOR A MEANS OF ESCAPE.  "\;
DATA             "I  LEFT THIS MESSAGE HIDDEN IN A BARREL INACCESSABLE TO THE BEAST IN THE NEXT ROOM, "\;
DATA             "FOR IT IS NECESSARY TO READ MY NAME TO BREAK THE SPELL, "\;
DATA             "AND TO THE BEST OF MY KNOWLEDGE, OTYUGHS CAN'T READ."\;
DATA             "\$0D\$0D"\;
DATA             "I LEFT IT IN THE HOPE THAT SOMEONE MIGHT FIND IT, SO WE COULD SEARCH FOR A WAY OUT TOGETHER.";

DATA READ_SCROLL "INSCRIBED ON THE PARCHMENT ARE THE MAGICAL WRITINGS FOR A SPELL THAT CLEANS THINGS.  "\;
DATA             "IT SEEMS VERY POWERFUL."\;
DATA             "\$0D\$0D"\;
DATA             "STRANGELY ENOUGH, YOU UNDERSTAND HOW TO INVOKE THE SPELL. "\;
DATA             "HOWEVER, YOU ARE CERTAIN THE SCROLL CAN BE USED ONLY ONCE.";

DATA READ_NOTHING "THERE IS NOTHING TO READ.";

FN VB_READ_FN() {
  IF (FOCUS_OBJ=OBJ_TABLET) {
    PRINT_WORDS(READ_TABLET);
  }
  ELSE IF (FOCUS_OBJ=OBJ_SCROLL) {
    PRINT_WORDS(READ_SCROLL);
  }
  ELSE {
    PRINT_WORDS(READ_NOTHING);
  }
}

