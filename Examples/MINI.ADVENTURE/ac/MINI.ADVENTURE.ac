# -----------------------------------
# MINI ADVENTURE
# COPYRIGHT (C) 2012
# ROBERT L. BOCCHINO JR.
# -----------------------------------
# MAIN PROGRAM
# -----------------------------------
VAR DONE:1=FALSE;
VAR DIED:1=FALSE;
VAR LAMP_ON:1=FALSE;
VAR OTYUGH_ALIVE:1=TRUE;

DATA PROMPT "\$0DWHAT NEXT? ";
DATA DIED_MSG "\$0DI'M SORRY, BUT I THINK YOU DIED.  BETTER LUCK NEXT TIME.\$0D";

FN MAIN() {
  VAR LEN:1;
  SET MON_PROMPT[0,1]=$80;
  SET CURRENT_RM=RM_ENTER;
  PRINT_RM(CURRENT_RM);
  WHILE (NOT DONE) {
    PRINT_STRING(PROMPT);
    SET LEN=GET_LINE();
    IF (LEN > 0) {
      PARSE_INPUT();  
      IF (NOT DONE) HANDLE_DARK();
      IF (NOT DONE) HANDLE_OTYUGH();
    }
  }  
  IF (DIED) {
    PRINT_WORDS(DIED_MSG);
  }
}

DATA RUMBLING "\$0DYOU HEAR A RUMBLING ROAR IN THE DISTANCE.  SUDDENLY, SOMETHING VERY LARGE IS EATING YOU FOR DINNER.\$0D";

# # OF TURNS IN DARK
VAR DARK_TURNS:1=0;
# MAX # OF TURNS ALLOWED IN DARK
CONST DARK_MAX 2;
FN HANDLE_DARK() {
  IF (IN_DARK()) {
    INCR DARK_TURNS;
    IF (DARK_TURNS > DARK_MAX) {
      PRINT_WORDS(RUMBLING);      
      SET DONE=TRUE;
      SET DIED=TRUE;
    }
  }
  ELSE {
    SET DARK_TURNS=0;
  }
}

DATA OT_DIE "\$0DWHILE YOU ARE STANDING THERE, THE OTYUGH ATTACKS WITH ITS TENTACLE.  "\;
DATA        "THERE'S NO TIME TO REACT, AND NO ESCAPE.  YOU ARE QUICKLY ENVELOPED AND PULLED INTO ITS MAW.\$0D";

# # OF TURNS CONFRONTING OTYUGH
VAR OT_TURNS:1=0;
# MAX # OF TURNS ALLOWED
CONST OT_MAX 2;
FN HANDLE_OTYUGH() {
  IF (CURRENT_RM=RM_OFFAL AND OTYUGH_ALIVE) {
    INCR OT_TURNS;
    IF (OT_TURNS > OT_MAX) {
      PRINT_WORDS(OT_DIE);
      SET DONE=TRUE;
      SET DIED=TRUE;
    }
  } ELSE {
    SET OT_TURNS=0;
  }
}

FN:1 IN_DARK() {
  RETURN (CURRENT_RM > RM_GRIMY) 
   AND ((NOT LAMP_ON) OR (NOT IS_HERE(OBJ_LAMP,CURRENT_RM)));
}

INCLUDE "MA.PARSING";
INCLUDE "MA.ROOMS";
INCLUDE "MA.OBJECT.DATA";
INCLUDE "MA.OBJECT.FNS";
INCLUDE "MA.OBJECT.FNS.2";
INCLUDE "MA.VERBS";
INCLUDE "MA.VERB.FNS";
INCLUDE "MA.VERB.FNS.2";
INCLUDE "MA.COMMANDS";
INCLUDE "IO";
INCLUDE "STRING";
INCLUDE "CHAR";
INCLUDE "PRINT.WORDS";
INCLUDE "PRINT.WORDS.ASM";