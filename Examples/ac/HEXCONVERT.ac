# -----------------------------------
# HEXCONVERT
# AN INTERACTIVE PROGRAM FOR
# DECIMAL-HEXADECIMAL CONVERSION
# WITH VARIABLE PRECISION
# -----------------------------------
# COPYRIGHT (C) 2011 
# ROBERT L. BOCCHINO JR.
# -----------------------------------
# SIZE OF DATA IN BYTES
# -----------------------------------
CONST DEFAULT_SIZE    2;
CONST MAX_SIZE        100;

VAR SIZE:1=DEFAULT_SIZE;
# -----------------------------------
# STRING OUTPUT
# -----------------------------------
DATA GREETING "HEXCONVERT\$0D"\;
DATA          "COPYRIGHT (C) 2011\$0D"\;
DATA          "ROBERT L. BOCCHINO JR.\$0D"\;
DATA          "TYPE H FOR HELP\$0D";
DATA HELP     "$([0-9]|[A-F]) - HEX TO DEC\$0D"\;
DATA          "[0-9]+         - DEC TO HEX\$0D"\;
DATA          "S[0-9]+        - SET SIZE IN BYTES\$0D"\;
DATA          "BLANK LINE     - END\$0D";
DATA ERROR    "ERROR";
# -----------------------------------
# MAIN FUNCTION
# -----------------------------------
FN MAIN() {
  VAR OK:1;
  VAR LEN:1;

  # PRINT GREETING
  PRINT_STRING(GREETING);

  # LOOP AND PROCESS INPUT TILL DONE
  WHILE (TRUE) {
    SET LEN=GET_LINE();
    IF (LEN=0) RETURN;
    SET OK=FALSE;
    IF ((MON_IN[0,1] AND $7F)='H') {
      IF (LEN=1) {
        PRINT_STRING(HELP);
        SET OK=TRUE;
      }
    }
    ELSE IF ((MON_IN[0,1] AND $7F)='S') {
      IF (LEN>1) {
        SET OK=SET_SIZE();
      }
    }
    ELSE IF ((MON_IN[0,1] AND $7F)='$') {
      IF (LEN>1) {
        SET OK=HEX_TO_DEC();
      }
    }
    ELSE {
      SET OK=DEC_TO_HEX();
    }
    IF (NOT OK) {
      PRINT_STRING(ERROR);
      MON_CROUT();
    }
  }
}
# -----------------------------------
# SET THE SIZE
# -----------------------------------
FN:1 SET_SIZE() {
  VAR OK:1;
  VAR NEW_SIZE:1;
  SET OK=STR_TO_NUM(MON_IN+1,10,@NEW_SIZE,1);
  IF (OK AND (NEW_SIZE>0) AND
   (NEW_SIZE<=MAX_SIZE)) {
    SET SIZE=NEW_SIZE;
    RETURN TRUE;
  }
  RETURN FALSE;
}
# -----------------------------------
# CONVERT HEX TO DEC
# -----------------------------------
FN:1 HEX_TO_DEC() {
  VAR OK:1;
  VAR VAL:2=ALLOCATE(SIZE);
  SET OK=STR_TO_NUM(MON_IN+1,16,VAL,SIZE);
  IF (OK) {
    PRINT_CHAR('=');
    NUM_TO_STR(VAL,SIZE,10,MON_IN);
    PRINT_STRING(MON_IN);
    MON_CROUT();
  }
  RETURN OK;
}
# -----------------------------------
# CONVERT DEC TO HEX
# -----------------------------------
FN:1 DEC_TO_HEX() {
  VAR OK:1;
  VAR VAL:2=ALLOCATE(SIZE);
  SET OK=STR_TO_NUM(MON_IN,10,VAL,SIZE);
  IF (OK) {
    PRINT_CHAR('=');
    PRINT_CHAR('$');
    NUM_TO_STR(VAL,SIZE,16,MON_IN);
    PRINT_STRING(MON_IN);
    MON_CROUT();
  }
  RETURN OK;
}

INCLUDE "IO";
INCLUDE "STRING";
INCLUDE "CHAR";