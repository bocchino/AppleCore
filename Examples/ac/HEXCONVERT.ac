# -----------------------------------
# HEX-DECIMAL CONVERSION
# -----------------------------------
CONST INBUF     $200;  # MONITOR INPUT BUFFER
CONST CR        $0D;   # CARRIAGE RETURN
CONST TRUE      1;
CONST FALSE     0;
CONST MON_CROUT $FD8E;
CONST SIZE      16;

DATA GREETING "HEXCONVERT\$0D"\;
DATA          "COPYRIGHT (C) 2011\$0D"\;
DATA	      "ROBERT L. BOCCHINO JR.\$0D"\;
DATA	      "ENTER BLANK LINE TO END\$0D";
DATA ERROR    "ERROR";

FN:1 GET_LINE();
FN   PRINT_CHAR(CHAR:1);
FN   NUM_TO_STR(N:2,SIZE:1,RADIX:1,STR:2);
FN:1 STR_TO_NUM(STR:2,RADIX:1,N:2,SIZE:1);
FN   PRINT_STRING(STR:2);
FN PRINT_HEX(N_PTR:2,SIZE:1);

FN MAIN() {
  VAR VAL:2=ALLOCATE(SIZE);
  VAR OK:1;
  VAR LEN:1;

  PRINT_STRING(GREETING);

  WHILE (TRUE) {
    SET OK=FALSE;
    SET LEN=GET_LINE();
    IF (LEN=0) RETURN;
    IF ((INBUF[0,1] AND $7F)='$') {
      IF (LEN > 1) {
        SET OK=STR_TO_NUM(INBUF+1,16,VAL,SIZE);
        IF (OK) {
          PRINT_CHAR('=');
          NUM_TO_STR(VAL,SIZE,10,INBUF);
	  PRINT_STRING(INBUF);
        }
      }
    }
    ELSE {
      SET OK=STR_TO_NUM(INBUF,10,VAL,SIZE);
      IF (OK) {
        PRINT_CHAR('=');
	PRINT_CHAR('$');
	NUM_TO_STR(VAL,SIZE,16,INBUF);
	PRINT_STRING(INBUF);
      }
    }
    IF (NOT OK) PRINT_STRING(ERROR);
    PRINT_CHAR(CR);
  }
}

INCLUDE "IO";
